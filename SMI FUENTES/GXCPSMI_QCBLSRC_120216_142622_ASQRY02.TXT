       IDENTIFICATION DIVISION.
      *-----------------------*
       PROGRAM-ID. ASQRY02.
       AUTHOR. JORGE DUARTE U.
       ENVIRONMENT DIVISION.
      *--------------------*
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.      IBM-AS400.
       OBJECT-COMPUTER.      IBM-AS400.
       SPECIAL-NAMES.        LOCAL-DATA IS LOCAL-DATA-AREA .
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT ASQRY09F    ASSIGN        TO DATABASE-ASQRY09F
                             ORGANIZATION  IS INDEXED
                             ACCESS MODE   IS DYNAMIC
                             RECORD KEY    IS EXTERNALLY-DESCRIBED-KEY
                             FILE STATUS   IS FS-ASQRY09F.

           SELECT ASQRY12F    ASSIGN        TO DATABASE-ASQRY12F
                             ORGANIZATION  IS INDEXED
                             ACCESS MODE   IS DYNAMIC
                             RECORD KEY    IS EXTERNALLY-DESCRIBED-KEY
                             FILE STATUS   IS FS-ASQRY12F.

           SELECT ASQRY18F    ASSIGN        TO DATABASE-ASQRY18F
                             ORGANIZATION  IS INDEXED
                             ACCESS MODE   IS DYNAMIC
                             RECORD KEY    IS EXTERNALLY-DESCRIBED-KEY
                             FILE STATUS   IS FS-ASQRY18F.

           SELECT PANTALLA1  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL01
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN01.

           SELECT PANTALLA2  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL02
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN02.

           SELECT PANTALLA3  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL03
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN03.

           SELECT PANTALLA4  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL04
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN04.

           SELECT PANTALLA5  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL05
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN05.

           SELECT PANTALLA6  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL06
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN06.

           SELECT PANTALLA7  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL07
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN07.

           SELECT PANTALLA8  ASSIGN TO WORKSTATION-ASQRY02D-SI
                             ORGANIZATION IS TRANSACTION
                             ACCESS MODE  IS DYNAMIC
                             RELATIVE KEY IS NREL08
                             CONTROL-AREA IS AREA-CONTROL
                             FILE STATUS  IS FS-PAN08.



       DATA DIVISION.
      *--------------*
       FILE SECTION.
      *-------------*
       FD  ASQRY09F  LABEL   RECORD STANDARD.
       01  R-ASQRY09F.
           COPY  DDS-ALL-FORMAT  OF ASQRY09F.

       FD  ASQRY12F  LABEL   RECORD STANDARD.
       01  R-ASQRY12F.
           COPY  DDS-ALL-FORMAT  OF ASQRY12F.

       FD  ASQRY18F  LABEL   RECORD STANDARD.
       01  R-ASQRY18F.
           COPY  DDS-ALL-FORMAT  OF ASQRY18F.

       FD PANTALLA1 LABEL RECORD IS OMITTED.
       01 R-PANTALLA1            PIC X(1920).

       FD PANTALLA2 LABEL RECORD IS OMITTED.
       01 R-PANTALLA2            PIC X(1920).

       FD PANTALLA3 LABEL RECORD IS OMITTED.
       01 R-PANTALLA3            PIC X(1920).

       FD PANTALLA4 LABEL RECORD IS OMITTED.
       01 R-PANTALLA4            PIC X(1920).

       FD PANTALLA5 LABEL RECORD IS OMITTED.
       01 R-PANTALLA5            PIC X(1920).

       FD PANTALLA6 LABEL RECORD IS OMITTED.
       01 R-PANTALLA6            PIC X(1920).

       FD PANTALLA7 LABEL RECORD IS OMITTED.
       01 R-PANTALLA7            PIC X(1920).

       FD PANTALLA8 LABEL RECORD IS OMITTED.
       01 R-PANTALLA8            PIC X(1920).

       WORKING-STORAGE SECTION.
      *------------------------*
       77 I-ON                PIC 1 VALUE B"1".
       77 I-OFF               PIC 1 VALUE B"0".
       77 WS-FLDCORTO         PIC X(12).
       77 WS-DATECUR          PIC 9(08).
       77 LARGO-WS            PIC S9(5).
       77 LARGO-REGISTRO      PIC S9(5).
       77 NUMPRO-WS           PIC S9(5).
       77 DESPRO-WS           PIC X(30).
       77 NSUBPRO-WS          PIC S9(5).
       77 WS-CTA-NUMPRO       PIC S9(5).
       77 NREG-F16            PIC S9(5).
       77 P-ARC-SEL           PIC S9(5).
       77 P-CAM-SEL           PIC S9(5).
       77 POS-SQL             PIC S9(5).
       77 POS-ARR-FILE        PIC S9(4).
       77 POS-ARR-FILE-T      PIC S9(4).
       77 POS-ARR-CAMPO       PIC S9(4).
       77 POS-ARR-CAMPO-T     PIC S9(4).
       77 CTA-CAMPO-SEL       PIC S9(4).
       77 POS-INI-SEL         PIC S9(6).
       77 POS-INI-SEL-F       PIC S9(6).
       77 USA-ARC-RUT         PIC X.
       77 EXISTE-FILE         PIC X.
       77 EXISTE-CAMPO        PIC X.
       77 SALIR-SQL           PIC X.
       77 WS-ARCSEL           PIC X(10).
       77 WS-CAMSEL           PIC X(10).
       77 WS-ARCGEN           PIC X(05).
       77 CONSQL-WS           PIC X(980).
       77 ARCHIVOS-SEL        PIC X(400).
       77 SELECCION-FINAL     PIC X(9000).
       77 SELECCION-FINAL-CON PIC X(10000).
       77 SELECCION-SQL       PIC X(1000).
       77 SELECCION-PEPA      PIC X(1000).
       77 SELECCION-TOTAL     PIC X(10000).
       77 ARC-REFERENCIA      PIC X(10).
       77 DATO-CONVERTIDO     PIC X(40).

       77 SEL-NPROC           PIC X(200).
       77 SEL-DPROC           PIC X(200).
       77 SEL-C1              PIC X(1000).
       77 SEL-ARC             PIC X.

       77 SEL-NUMRUT          PIC X.
       77 SEL-DEUTOT1         PIC X.
       77 RECSEL              PIC X(3000).
       77 CEROS               PIC S9(03).

       77 ARCHIVOR            PIC X(10).
       77 ARCHIVOT            PIC X(10).
       77 DROPR               PIC X(100).
       77 DROPT               PIC X(100).

       77 RET-Y               PIC 9(7).
       77 Y                   PIC 9(7).
       77 POS-ARR-SA          PIC 9(7).
       77 INI-STR             PIC 9(7).
       77 INI-STR-AP          PIC 9(7).
       77 SALIR-CICLO         PIC X.
       77 ORDEN-MALO          PIC X.
       77 SAL-WS              PIC X.

       77 PRA-PARTE-SQL       PIC X(36).
       77 PRA-PARTE-SQL1      PIC X(36).
       77 PRA-PARTE-SQL-LAB   PIC X(39).

       77 CREA-CAMPO          PIC X(100).
       77 CREA-CAMPO-LAB      PIC X(66).
       77 CREA-CAMPO-TXT      PIC X(71).

       77 AGREGA-1            PIC X(500).
       77 TOTAL-CREA-CAMPO    PIC X(15000).
       77 TOTAL-CREA-CAMPO-1  PIC X(15000).
       77 TOT-CAMPO-LAB       PIC X(15000).
       77 ALT-PAGO-01         PIC X(300).
       77 ALT-PAGO-02         PIC X(300).
       77 ALT-PAGO-03         PIC X(300).
       77 ALT-PAGO-04         PIC X(300).

       77 PARTEFINAL          PIC X(19000).
       77 PARTEFINAL1         PIC X(19000).
       77 PARTELAB            PIC X(19000).

       77 PROCESO-WS          PIC X(10).
       77 ARCHIVO-WS          PIC X(10).

       77 ARCWC02             PIC X(10).
       77 BIBWC02             PIC X(10).
       77 CAMWC02             PIC X(10).
       77 REFWC02             PIC X(22).
       77 TIPWC02             PIC X(17).
       77 LARWC02             PIC S9(05).
       77 DECWC02             PIC S9(02).

       77 WS-H-ARCHIVO        PIC X(10).
       77 WS-H-CAMPO          PIC X(10).
       77 WS-H-SENSQL         PIC X(30).
       77 WS-H-ARC-CAMPO      PIC X(21).
       77 WS-H-PC             PIC 9(04).
       77 WS-FILA             PIC 9(03).
       77 WS-COLU             PIC 9(03).

       77 ARCHIVO-SEL         PIC X(10).
       77 CAMPO-SEL           PIC X(10).
       77 P-S                 PIC 9(4).
       77 P-FLD               PIC 9(4).
       77 PINI-FLD            PIC 9(5).
       77 PINI-COM            PIC 9(5).
       77 SALIR-FLD           PIC X.
       77 ARCFLD              PIC X(22).
       77 ORDEN-PEPA          PIC 9(5).
       77 U-PEPA              PIC S9(4).
       77 FUNCION-WS          PIC X.
       77 ARCHIVO-PEPA        PIC X(10).
       77 CAMPO-PEPA          PIC X(10).
       77 P-WS02              PIC 9(4).

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

          05 AREA-CONTROL.
             06 MDTO                PIC X(02).
                88 F3               VALUE "03".
                88 F4               VALUE "04".
                88 F5               VALUE "05".
                88 F6               VALUE "06".
                88 F7               VALUE "07".
                88 F9               VALUE "09".
                88 F12              VALUE "12".
                88 F13              VALUE "13".
                88 F15              VALUE "15".
                88 F23              VALUE "23".

       01 VARIABLES-DE-STATUS.
          05 FS-ASQRY09F    PIC XX VALUE "00".
          05 FS-ASQRY12F    PIC XX VALUE "00".
          05 FS-ASQRY18F    PIC XX VALUE "00".
          05 FS-PAN01      PIC XX.
          05 FS-PAN02      PIC XX.
          05 FS-PAN03      PIC XX.
          05 FS-PAN04      PIC XX.
          05 FS-PAN05      PIC XX.
          05 FS-PAN06      PIC XX.
          05 FS-PAN07      PIC XX.
          05 FS-PAN08      PIC XX.

       01 VARIABLES-RELATIVAS.
          05 NREL01              PIC 9(03).
          05 NREL02              PIC 9(03).
          05 NREL03              PIC 9(03).
          05 NREL04              PIC 9(03).
          05 NREL05              PIC 9(03).
          05 NREL06              PIC 9(03).
          05 NREL07              PIC 9(03).
          05 NREL08              PIC 9(03).
          05 P1                  PIC 9(05).
          05 I                   PIC 9(05).
          05 ARCHIVOS-USAR       PIC X(10).
          05 CANTIDAD-ARC-SEL    PIC 9(05).
          05 ARCHIVO-12          PIC X(12).
          05 INCLUDE-ARC-SEL     PIC X(1000).
          05 WSELECCION          PIC X(2000).

       01 PARAM.
          05 ARREGLO-ORDEN  OCCURS 999 TIMES.
             10 NRO-VECES          PIC 9(03).
             10 DESCR-ARR          PIC X(40).
             10 CAMPO-LARGO-ARR    PIC X(18).
             10 CAMPO-CORTO-ARR    PIC X(12).
             10 CAMPO-REFER-ARR    PIC X(22).
             10 LARGO-ARR          PIC 9(05).
             10 DIGIT-ARR          PIC 9(05).
             10 DECIM-ARR          PIC 9(02).
             10 TIPO-ARR           PIC X(17).

          05 ARREGLO-ORDEN-SIN-ARCHIVO OCCURS 999 TIMES.
             10 NRO-VECES-SA       PIC 9(03).
             10 DESCR-ARR-SA       PIC X(40).
             10 CAMPO-LARGO-ARR-SA PIC X(18).
             10 CAMPO-CORTO-ARR-SA PIC X(12).
             10 CAMPO-REFER-ARR-SA PIC X(22).
             10 LARGO-ARR-SA       PIC 9(05).
             10 DIGIT-ARR-SA       PIC 9(05).
             10 DECIM-ARR-SA       PIC 9(02).
             10 TIPO-ARR-SA        PIC X(17).
          05 WS-ARCHIVO            PIC X(10).
          05 WS-BIBLIOTECA         PIC X(10).

       01 OTROS-ARREGLOS-DE-JOB.
          05 CAMPOS-SEL-ARR  OCCURS 999 TIMES.
             10 ARCHIVO-S        PIC X(10).
             10 CAMPO-S          PIC X(10).

           05 ARREGLO-WS02 OCCURS 999 TIMES.
              10 ARCHIVO-WS02    PIC X(10).
              10 CAMPO-WS02      PIC X(10).
              10 DCAMPO-WS02     PIC X(50).
              10 ORDEN-WS02      PIC S9(3).
              10 FLDREF-WS02     PIC X(22).
              10 LARGO-WS02      PIC S9(5).
              10 DIGIT-WS02      PIC S9(5).
              10 DECIM-WS02      PIC S9(2).
              10 TIPO-WS02       PIC X(17).

       01 REG-SQL.
          05 SELFLD-SQL          PIC X(52).
          05 LARGO-SQL           PIC S9(05).
          05 DIGIT-SQL           PIC S9(02).
          05 DECIM-SQL           PIC S9(02).
          05 TIPO-SQL            PIC X(17).

       01 WS00-OO.
          COPY DDS-WS00-O       OF ASQRY02D.

       01 WC00-II.
          COPY DDS-WC00-O       OF ASQRY02D.

       01 WC00-IND.
          COPY DDS-WC00-INDIC   OF ASQRY02D.

       01 WS01-OO.
          COPY DDS-WS01-O       OF ASQRY02D.

       01 WC01-II.
          COPY DDS-WC01-I       OF ASQRY02D.

       01 WC01-IND.
          COPY DDS-WC01-INDIC   OF ASQRY02D.

       01 WS02-OO.
          COPY DDS-WS02-O       OF ASQRY02D.

       01 WS02-IND.
          COPY DDS-WS02-INDIC   OF ASQRY02D.

       01 WC02-II.
          COPY DDS-WC02-I       OF ASQRY02D.

       01 WC02-IND.
          COPY DDS-WC02-INDIC   OF ASQRY02D.

       01 WS03-OO.
          COPY DDS-WS03-O       OF ASQRY02D.

       01 WC03-II.
          COPY DDS-WC03-O       OF ASQRY02D.

       01 WC03-IND.
          COPY DDS-WC03-INDIC   OF ASQRY02D.

       01 WS04-OO.
          COPY DDS-WS04-O       OF ASQRY02D.

       01 WC04-II.
          COPY DDS-WC04-O       OF ASQRY02D.

       01 WC04-IND.
          COPY DDS-WC04-INDIC   OF ASQRY02D.

       01 W01-II.
          COPY DDS-W01-I        OF ASQRY02D.

       01 W01-IND.
          COPY DDS-W01-INDIC    OF ASQRY02D.

       01 W02-II.
          COPY DDS-W02-I        OF ASQRY02D.

       01 W02-IND.
          COPY DDS-W02-INDIC    OF ASQRY02D.

       01 W03-II.
          COPY DDS-W03-I        OF ASQRY02D.

       01 W03-IND.
          COPY DDS-W03-INDIC    OF ASQRY02D.

       01 DATOS-CURSOR-C1.
          05 NROPRO-C1         PIC S9(5).
          05 NOMPRO-C1         PIC X(30).
          05 CANSPR-C1         PIC S9(5).

       01 DATOS-CURSOR-C2.
          05 CORPRO-C2         PIC S9(5).
          05 DCOPRO-C2         PIC X(50).

       01 DATOS-CURSOR-PEPA.
          05 NROPEP-PEPA       PIC S9(4).
          05 DESPEP-PEPA       PIC X(50).

       01 DATOS-CURSOR-CPEPA.
          05 NROPEP-PEPA1      PIC S9(4).
          05 DESPEP-PEPA1      PIC X(50).
          05 WHFLDB-PEPA1      PIC S9(5).
          05 WHFLDD-PEPA1      PIC S9(5).
          05 WHFLDP-PEPA1      PIC S9(2).
          05 WHFLDT-PEPA1      PIC X.


       01 PARA-ASQRY05.
          05 NOMAYU-16C          PIC X(10).
          05 SELECC-16C          PIC X(500).
          05 RETORNO-16C.
             10 CODIGO-16C          PIC X(500).
             10 LARGOKEY-16C        PIC 9(03).
             10 LARGODES-16C        PIC 9(03).

       01 VARIABLE-QCMDEXC.
           05 MANDATO-200    PIC X(200).
           05 LARGO-200      PIC S9(10)V9(5) USAGE IS COMP VALUE 200.

      *
       01 PARA-ASQRY07.
          05 NOMAYU-18C          PIC X(10).
          05 SELECC-18C          PIC X(500).
          05 LARGOKEY-18C        PIC 9(03).
          05 LARGODES-18C        PIC 9(03).
          05 RETORNO-18C         PIC X(30000).
          05 MARCAR-18C          PIC X(30000).

       01 ARREGLOS-DE-TRABAJO.
          05 CTA-MSG              PIC 9(3).
          05 VARIABLE-3250        PIC X(6500).
          05 VAR-ARR-3250 REDEFINES VARIABLE-3250 OCCURS 100 TIMES
                          ASCENDING KEY IS TEXTO-ARR.
             10 TEXTO-ARR         PIC X(65).

          05 ARCHIVOS-SELECCION OCCURS 100 TIMES.
             10 ARCHIVO-ARR       PIC X(10).

          05 CAMPOS-SELECCION OCCURS 999 TIMES.
             10 CAMPO-ARR         PIC X(22).
             10 CAMPO-ARR-CON     PIC X(55).

       01 ARREGLO-SELECCION.
          05 ARR-SEL-X          PIC X(30000).
          05 ARR-SEL REDEFINES ARR-SEL-X OCCURS 240 TIMES.
             15 CODIGO-X        PIC X(125).

          05 ARR-MAR-X          PIC X(30000).
          05 ARR-MAR REDEFINES ARR-MAR-X OCCURS 240 TIMES.
             15 CODMAR-X        PIC X(125).

       01 VARIABLES-DE-TRABAJO.
          05 WS-HHMMSSSS.
             10 WS-HORA         PIC S9(6).
             10 FILLER          PIC S9(2).

          05 AAAAMMDD           PIC X(8).
          05 AAAAMMDD-9 REDEFINES AAAAMMDD PIC S9(8).
          05 DDMMAAAA           PIC X(8).

       01 PARAMETROS-10A.
           05 NOMARC-10A          PIC X(10).
           05 BIBARC-10A          PIC X(10).
           05 TEXTO-10A           PIC X(50).
           05 OPCION-10A          PIC X(01).
           05 RET-10A             PIC X(01).
           05 MBR-10A             PIC X(10).
      *
       LINKAGE SECTION.
      *----------------
       01 REG-LNK.
          05 USUARIO-LNK    PIC X(10).

       PROCEDURE DIVISION USING REG-LNK.
      *-----------------------------------

           PERFORM INICIO
           PERFORM PROCESO-WC00 UNTIL F3
           PERFORM TERMINO
           GOBACK.

      *
       INICIO.
      *-------
      * PANTALLA1 -> WC00 WS00
      * PANTALLA2 -> WC01 WS01
      * PANTALLA3 -> WC02 WS02
      * PANTALLA4 -> WC03 WS03
      * PANTALLA5 -> W01
      * PANTALLA6 -> W02
      *
           OPEN INPUT ASQRY09F
                I-O   PANTALLA1 PANTALLA2 PANTALLA3 PANTALLA4
                      PANTALLA5 PANTALLA6 PANTALLA7 PANTALLA8.

           INITIALIZE WC00-O REPLACING
                      ALPHANUMERIC DATA BY SPACES
                      NUMERIC      DATA BY ZEROES.
           PERFORM CARGAR-WS00
           CALL "MRTVDAF1" USING WS-DATECUR
           MOVE WS-DATECUR       TO DATECUR OF WC00-II
           MOVE ZEROES           TO MDTO.

      *
       PROCESO-WC00.
      *-------------
           WRITE R-PANTALLA1 FROM WC00-II FORMAT "WC00"
                             INDICATORS ARE WC00-O-INDIC END-WRITE
           READ  PANTALLA1   INTO WC00-II FORMAT "WC00"
                             INDICATORS ARE WC00-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG
           IF F9
              PERFORM INSERTA-PROCESO
           ELSE
           IF F7
              PERFORM MODIFICA-PROCESO
           ELSE
           IF F23
              PERFORM ELIMINA-PROCESO.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
           PERFORM CARGAR-WS00.
      *
       PROCESO-WC01.
      *-------------
           WRITE R-PANTALLA2 FROM WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-O-INDIC END-WRITE
           READ  PANTALLA2   INTO WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG.
      *
       PROCESO-WC02.
      *-------------
           WRITE R-PANTALLA3 FROM WC02-II FORMAT "WC02"
                             INDICATORS ARE WC02-O-INDIC END-WRITE
           READ  PANTALLA3   INTO WC02-II FORMAT "WC02"
                             INDICATORS ARE WC02-I-INDIC END-READ.
           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG.
      *
       PROCESO-WC03.
      *-------------
           WRITE R-PANTALLA4 FROM WC03-II FORMAT "WC03"
                             INDICATORS ARE WC03-O-INDIC END-WRITE
           READ  PANTALLA4   INTO WC03-II FORMAT "WC03"
                             INDICATORS ARE WC03-I-INDIC END-READ.
           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG.
           IF F9
              PERFORM VALIDA-PROCESO
              IF CTA-MSG = 0
                 PERFORM NUEVO-SUBPROCESO
                 MOVE I-ON       TO IN50    OF WC03-O-INDIC
              END-IF
           ELSE
           IF F7
              PERFORM MODIFICA-SUBPROCESO
           ELSE
           IF F23
              PERFORM ELIMINA-SUBPROCESO.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
           PERFORM CARGAR-WS03.
      *
       PROCESO-W01.
      *-------------
           WRITE R-PANTALLA5 FROM W01-II FORMAT "W01"
                             INDICATORS ARE W01-O-INDIC END-WRITE
           READ  PANTALLA5   INTO W01-II FORMAT "W01"
                             INDICATORS ARE W01-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG

           IF F4
              PERFORM AYUDA-PROCESO-SUBPRO
           ELSE
           IF F5
              PERFORM CAMBIO-DE-CONDICIONES
              MOVE ZEROES    TO MDTO
           ELSE
           IF F9
              MOVE CORPRO OF W01-II    TO NSUBPRO-WS
              PERFORM VALIDA-W01
              IF CTA-MSG = ZEROES
                 STRING "B"    DELIMITED SIZE
                        ARCGEN OF W01-II DELIMITED BY " "
                        "BASE"           DELIMITED SIZE
                                       INTO ARCBAS  OF W01-II
                 MOVE CORPRO OF W01-II   TO NSUBPRO-WS
                 PERFORM GRABA-REGRABA-ASQRY09F
                 PERFORM SELECCION-ARCHIVOS-UTILIZAR
                 IF RETORNO-18C NOT = SPACES
                    MOVE "S"    TO SEL-ARC
                    PERFORM CONTINUA-SELECCION
                    MOVE I-ON               TO IN50 OF W01-O-INDIC
                    MOVE ZEROES   TO MDTO
                 ELSE
                    IF PROREF OF W01-II NOT = 0 AND
                       CPRREF OF W01-II NOT = 0
                       MOVE "N"    TO SEL-ARC
                       PERFORM CONTINUA-SELECCION
                       MOVE I-ON               TO IN50 OF W01-O-INDIC
                       MOVE ZEROES   TO MDTO.


           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       PROCESO-W02.
      *-------------
           WRITE R-PANTALLA6 FROM W02-II FORMAT "W02"
                             END-WRITE
           READ  PANTALLA6   INTO W02-II FORMAT "W02"
                             INDICATORS ARE W02-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG

           IF F4
              PERFORM AYUDA-SENTENCIA-SQL
           ELSE
           IF F6
              PERFORM AYUDA-CAMPOS-FILE
           ELSE
           IF F9
              PERFORM VALIDA-W02
              IF CTA-MSG = 0
                 PERFORM ACTUALIZA-SEN-SQL.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       VALIDA-PROCESO.
      *---------------
           IF IN50 OF WC03-O-INDIC = I-OFF
              MOVE NUMPRO OF WC03-II TO NUMPRO-WS
              MOVE ZEROES     TO WS-CTA-NUMPRO

           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY10F WHERE NROPRO = :NUMPRO-WS
           END-EXEC

           IF WS-CTA-NUMPRO > 0
              ADD   1   TO    CTA-MSG
              MOVE "PROCESO YA SE ENCUENTRA INGRESADO" TO
                   TEXTO-ARR(CTA-MSG).

      *
       VALIDA-W01.
      *-----------
           IF IN50 OF W01-O-INDIC = I-OFF

           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY09F WHERE NROPRO = :NUMPRO-WS AND CORPRO =
           :NSUBPRO-WS
           END-EXEC

              IF WS-CTA-NUMPRO > 0
                 ADD   1           TO CTA-MSG
                 MOVE "SUBPROCESO YA SE ENCUENTA INGRESADO"
                                              TO TEXTO-ARR(CTA-MSG)
              END-IF
           END-IF

           IF CORPRO OF W01-I = ZEROES
              ADD   1           TO CTA-MSG
              MOVE "SUB-PROCESO DEBE SER > 0" TO TEXTO-ARR(CTA-MSG)
           END-IF
           IF DCOPRO OF W01-I = SPACES
              ADD   1           TO CTA-MSG
              MOVE "FALTA DESCIRPCION PARA SUB-PROCESO "
                                              TO TEXTO-ARR(CTA-MSG)
           END-IF
           IF ARCGEN OF W01-I = SPACES
              ADD   1           TO CTA-MSG
              MOVE "FALTA CARACTERES DE ARCHIVO A GENERAR"
                                              TO TEXTO-ARR(CTA-MSG)
           ELSE
              IF ARCGEN OF W01-II(1:1) = "A" OR "B" OR "C" OR "D" OR
                                      "E" OR "F" OR "G" OR "H" OR
                                      "I" OR "J" OR "K" OR "L" OR
                                      "M" OR "N" OR "O" OR "P" OR
                                      "Q" OR "R" OR "S" OR "T" OR
                                      "U" OR "V" OR "W" OR "X" OR
                                      "Y" OR "Z"
                 MOVE ARCGEN OF W01-II TO WS-ARCGEN
                 PERFORM CHEQUEA-NOMBRE
              ELSE
                 ADD   1           TO CTA-MSG
                 STRING "PRIMER CARACTER DE ARCHIVO " DELIMITED SIZE
                        "A GENERAR DEBE SER UNA LETRA" DELIMITED SIZE
                                            INTO TEXTO-ARR(CTA-MSG)
              END-IF
           END-IF
           IF CORGEN OF W01-I = ZEROES
              ADD   1           TO CTA-MSG
              MOVE "CORRELATIVO INICIAL DEBE SER > 0     "
                                              TO TEXTO-ARR(CTA-MSG)
           END-IF.

      *
       CHEQUEA-NOMBRE.
      *---------------
           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY09F WHERE NROPRO <> :NUMPRO-WS AND CORPRO
           <> :NSUBPRO-WS AND ARCGEN = :WS-ARCGEN
           END-EXEC

           IF WS-CTA-NUMPRO > 0
              ADD   1           TO CTA-MSG
              MOVE "NOMBRE ARCHIVO A GENERAR YA SE ENCUENTRA INGRESADO"
                                          TO TEXTO-ARR(CTA-MSG)
           END-IF.
      *
       VALIDA-W02.
      *-----------
           MOVE SPACES          TO SELECCION-SQL
           MOVE SENSQL01        TO SELECCION-SQL

            IF SELECCION-SQL = " "
               ADD  1           TO CTA-MSG
               MOVE "DEBE POSEER UN FILTRO DE SELECCION"
                    TO TEXTO-ARR(CTA-MSG)
            END-IF
            PERFORM VARYING POS-SQL FROM 1 BY 1 UNTIL POS-SQL > 980
                    OR SALIR-SQL = "S"
               IF SELECCION-SQL(POS-SQL:1) = "'"
                  ADD  1           TO CTA-MSG
                MOVE "DEBE SELECCION COMILLAS DOBLES EN VEZ DE SIMPLES"
                       TO TEXTO-ARR(CTA-MSG)
               END-IF
            END-PERFORM.
      *
       CAMBIO-DE-CONDICIONES.
      *----------------------
           MOVE NUMPRO-WS          TO NROPRO OF ASQRY09F
           MOVE CORPRO OF W01-II   TO CORPRO OF ASQRY09F
                                      NSUBPRO-WS
           READ ASQRY09F END-READ
           IF FS-ASQRY09F = "00"
              MOVE CONSQL OF ASQRY09F TO CONSQL-WS
              IF CONSQL-WS = SPACES
                 ADD  1           TO CTA-MSG
                 MOVE "NO EXISTE CONDICION GENERADA."
                      TO TEXTO-ARR(CTA-MSG)
              ELSE
                 MOVE ZEROES     TO MDTO
                 MOVE CONSQL-WS  TO SENSQL01 OF W02-II
                 PERFORM GENERA-ARCHIVO-SEL
                 PERFORM PROCESO-W02 UNTIL F9 OR F3.

      *
       GENERA-ARCHIVO-SEL.
      *-------------------
           OPEN INPUT ASQRY12F.
           MOVE NUMPRO-WS   TO NROPRO OF ASQRY12F
           MOVE NSUBPRO-WS  TO CORPRO OF ASQRY12F
           MOVE SPACES      TO NOMARC OF ASQRY12F
           START ASQRY12F KEY IS NOT < EXTERNALLY-DESCRIBED-KEY
                 END-START
           IF FS-ASQRY12F = '00'
              PERFORM LEER-NEXT-ASQRY12F
               MOVE SPACES     TO ARCHIVOS-SEL
               MOVE ' IN('     TO ARCHIVOS-SEL(1:4)
               MOVE 5          TO P-ARC-SEL
               MOVE 1          TO NREG-F16
               PERFORM UNTIL FS-ASQRY12F NOT = '00'
                       OR NROPRO OF ASQRY12F NOT = NUMPRO-WS
                       OR CORPRO OF ASQRY12F NOT = NSUBPRO-WS
                       IF NREG-F16 > 1
                          MOVE ', ' TO ARCHIVOS-SEL(P-ARC-SEL:2)
                          ADD  2    TO P-ARC-SEL
                       END-IF

                       MOVE '"'  TO ARCHIVOS-SEL(P-ARC-SEL:1)
                       ADD   1   TO P-ARC-SEL
                       MOVE NOMARC OF ASQRY12F
                                 TO ARCHIVOS-SEL(P-ARC-SEL:10)
                       ADD  10   TO P-ARC-SEL
                       MOVE '"'  TO ARCHIVOS-SEL(P-ARC-SEL:1)
                       ADD   1   TO P-ARC-SEL NREG-F16

                   PERFORM LEER-NEXT-ASQRY12F
               END-PERFORM
               MOVE ') '         TO ARCHIVOS-SEL(P-ARC-SEL:2).
            CLOSE ASQRY12F.

      *
       LEER-NEXT-ASQRY12F.
      *------------------
           READ ASQRY12F NEXT RECORD END-READ.
      *
       ACTUALIZA-SEN-SQL.
      *------------------
           IF RUNICO OF W01-II = 1
              MOVE 'DISTINCT'   TO SELECCION-FINAL(8:8)
           ELSE
              MOVE SPACES       TO SELECCION-FINAL(8:8)
           END-IF
           MOVE SPACES             TO SELECCION-TOTAL
           STRING SELECCION-FINAL  DELIMITED SIZE
                  ' WHERE '        DELIMITED SIZE
                  SELECCION-SQL    DELIMITED SIZE
                              INTO SELECCION-TOTAL
            EXEC SQL
           UPDATE  ASQRY09F SET SELECC = :SELECCION-TOTAL,
           CONSQL = :SELECCION-SQL, UARRUT = :USA-ARC-RUT, SELCON =
           :SELECCION-FINAL-CON, LARSEL = :LARGO-REGISTRO WHERE NROPRO
           = :NUMPRO-WS AND CORPRO = :NSUBPRO-WS
            END-EXEC.
      *
       INSERTA-PROCESO.
      *----------------
           INITIALIZE WC03-O REPLACING
                      ALPHANUMERIC DATA BY SPACES
                      NUMERIC DATA BY ZEROES.
           PERFORM CARGAR-WS03
           MOVE WS-DATECUR       TO DATECUR OF WC03-II
           MOVE I-OFF            TO IN50    OF WC03-O-INDIC
           MOVE ZEROES           TO MDTO
           PERFORM PROCESO-WC03 UNTIL F12
           MOVE ZEROES           TO MDTO.
      *
       MODIFICA-PROCESO.
      *----------------
           READ SUBFILE PANTALLA1 NEXT MODIFIED INTO WS00-OO
                        FORMAT "WS00" END-READ
           IF FS-PAN01 = "00"
              MOVE NROPRO OF WS00-OO TO NUMPRO  OF WC03-II
              MOVE DESPRO OF WS00-OO TO NOMPRO  OF WC03-II
              MOVE CANSPR OF WS00-OO TO NSUBPRO OF WC03-II
              PERFORM CARGAR-WS03
              MOVE WS-DATECUR       TO DATECUR OF WC03-II
              MOVE I-ON             TO IN50    OF WC03-O-INDIC
              MOVE ZEROES           TO MDTO
              PERFORM PROCESO-WC03 UNTIL F12
              MOVE ZEROES           TO MDTO.
      *
       ELIMINA-PROCESO.
      *----------------
           READ SUBFILE PANTALLA1 NEXT MODIFIED INTO WS00-OO
                        FORMAT "WS00" END-READ
           IF FS-PAN01 = "00"
              MOVE NROPRO OF WS00-OO   TO NUMPRO-WS
              PERFORM CHEQUEA-REF-NUMPRO
              IF CTA-MSG = 0
                 PERFORM ELIMINA-PROCESO-SUBPROCESO.
      *
       CHEQUEA-REF-NUMPRO.
      *-------------------
           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY07F WHERE NROPRO = :NUMPRO-WS
           END-EXEC.

           IF WS-CTA-NUMPRO > 0
              ADD    1      TO CTA-MSG
              STRING "NO SE PUEDE ELIMINAR PROCESO " DELIMITED SIZE
                     NUMPRO-WS                       DELIMITED SIZE
                     " POR ESTAR EN EVENTO(S) "      DELIMITED SIZE
                                       INTO TEXTO-ARR(CTA-MSG)
           END-IF

           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY09F WHERE NROPRO <> :NUMPRO-WS AND PROREF
           = :NUMPRO-WS
           END-EXEC

           IF WS-CTA-NUMPRO > 0
              ADD    1      TO CTA-MSG
              STRING "NO SE PUEDE ELIMINAR PROCESO "   DELIMITED SIZE
                     NUMPRO-WS                         DELIMITED SIZE
                     " POR ESTAR COMO REF. DE PROCESO" DELIMITED SIZE
                                       INTO TEXTO-ARR(CTA-MSG)
           END-IF.

      *
       ELIMINA-PROCESO-SUBPROCESO.
      *---------------------------
           EXEC SQL
           DELETE FROM  ASQRY01F WHERE WHFILE IN(SELECT ARCBAS
           FROM ASQRY09F WHERE NROPRO = :NUMPRO-WS)
           END-EXEC

           EXEC SQL
           DELETE FROM  ASQRY09F WHERE NROPRO = :NUMPRO-WS
           END-EXEC

           EXEC SQL
           DELETE FROM  ASQRY10F WHERE NROPRO = :NUMPRO-WS
           END-EXEC.
           EXEC SQL
           DELETE FROM  ASQRY12F WHERE NROPRO = :NUMPRO-WS
           END-EXEC.

           ADD    1      TO CTA-MSG
           STRING "SE HA ELIMINADO PROCESO N° "   DELIMITED SIZE
                   NUMPRO-WS                      DELIMITED SIZE
                                    INTO TEXTO-ARR(CTA-MSG).
      *
       NUEVO-SUBPROCESO.
      *-----------------
           INITIALIZE W01-I REPLACING
                     ALPHANUMERIC DATA BY SPACES
                     NUMERIC      DATA BY ZEROES
           MOVE WS-DATECUR        TO DATECUR OF W01-II
           MOVE I-OFF             TO IN50    OF W01-O-INDIC
           MOVE ZEROES            TO MDTO
           MOVE NUMPRO OF WC03-II TO NUMPRO-WS
           MOVE ZEROES            TO NSUBPRO-WS
           MOVE NOMPRO OF WC03-II TO DESPRO OF W01-II
                                     DESPRO-WS
           CALL   "MRTVDAF2" USING AAAAMMDD DDMMAAAA
           CANCEL "MRTVDAF2"
           ACCEPT WS-HHMMSSSS FROM TIME


           EXEC SQL
           INSERT INTO  ASQRY10F (NROPRO, NOMPRO, CANSPR,
           FECCRE, HORCRE, USRCRE, FECMOD, HORMOD, USRMOD)
           VALUES(:NUMPRO-WS, :DESPRO-WS, 0, :AAAAMMDD-9, :WS-HORA,
           :USUARIO-LNK, :AAAAMMDD-9, :WS-HORA, :USUARIO-LNK)
           END-EXEC


           MOVE SPACES            TO CONSQL-WS
           MOVE I-OFF             TO IN50 OF W01-O-INDIC
           MOVE 1                 TO RARC OF W01-II
                                     RJOB OF W01-II
                                     AREG OF W01-II
                                     RUNICO OF W01-II
           PERFORM PROCESO-W01 UNTIL F12
           MOVE ZEROES            TO MDTO.
      *
       MODIFICA-SUBPROCESO.
      *-----------------
           INITIALIZE W01-I REPLACING
                     ALPHANUMERIC DATA BY SPACES
                     NUMERIC      DATA BY ZEROES
           READ SUBFILE PANTALLA4 NEXT MODIFIED INTO WS03-OO
                        FORMAT "WS03" END-READ
           IF FS-PAN04 = "00"
              MOVE NUMPRO OF WC03-II   TO NROPRO OF ASQRY09F
                                          NUMPRO-WS
              MOVE NSUBPRO OF WS03-OO  TO CORPRO OF ASQRY09F
                                          NSUBPRO-WS
              READ ASQRY09F END-READ
              IF FS-ASQRY09F = "00"
                 MOVE NOMPRO OF WC03-II TO DESPRO OF W01-II
                                           DESPRO-WS
                 PERFORM ACTUALIZA-DES-PROCESO
                 MOVE CORR RASQRY09F TO W01-I
                 MOVE CONSQL OF ASQRY09F TO CONSQL-WS
                 MOVE SELECC OF ASQRY09F TO SELECCION-FINAL
                 MOVE SELCON OF ASQRY09F TO SELECCION-FINAL-CON
                 MOVE WS-DATECUR        TO DATECUR OF W01-II
                 MOVE I-ON              TO IN50    OF W01-O-INDIC
                 MOVE ZEROES            TO MDTO
                 IF REEARC OF ASQRY09F = "S"
                    MOVE 1              TO RARC OF W01-II
                 ELSE
                    MOVE 2              TO RARC OF W01-II
                 END-IF
                 IF RETJOB OF ASQRY09F = "S"
                    MOVE 1              TO RJOB OF W01-II
                 ELSE
                    MOVE 2              TO RJOB OF W01-II
                 END-IF
                 IF ADDREG OF ASQRY09F = "S"
                    MOVE 1              TO AREG OF W01-II
                 ELSE
                    MOVE 2              TO AREG OF W01-II
                 END-IF
                 IF REGUNI OF ASQRY09F = "S"
                    MOVE 1              TO RUNICO OF W01-II
                 ELSE
                    MOVE 2              TO RUNICO OF W01-II
                 END-IF
                 PERFORM PROCESO-W01 UNTIL F12
                 MOVE ZEROES            TO MDTO.
      *
       ACTUALIZA-DES-PROCESO.
      *----------------------
           EXEC SQL
           UPDATE  ASQRY10F SET NOMPRO = :DESPRO-WS WHERE NROPRO
           = :NUMPRO-WS
           END-EXEC.

      *
       ELIMINA-SUBPROCESO.
      *-----------------
           READ SUBFILE PANTALLA4 NEXT MODIFIED INTO WS03-OO
                        FORMAT "WS03" END-READ
           IF FS-PAN04 = "00"
              MOVE NUMPRO  OF WC03-II TO NUMPRO-WS
              MOVE NSUBPRO OF WS03-OO TO NSUBPRO-WS
              PERFORM CHEQUEA-REF-NUMPRO-NSUBPRO
              IF CTA-MSG = 0
                 PERFORM ELIMINA-SUBPROCESO-1.
      *
       CHEQUEA-REF-NUMPRO-NSUBPRO.
      *---------------------------
           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY09F WHERE NROPRO <> :NUMPRO-WS AND CORPRO
           <> :NSUBPRO-WS AND PROREF = :NUMPRO-WS AND CPRREF =
           :NSUBPRO-WS
           END-EXEC

           IF WS-CTA-NUMPRO > 0
              ADD    1      TO CTA-MSG
              STRING "NO SE PUEDE ELIMINAR SUBPROCESO " DELIMITED SIZE
                     NUMPRO-WS                          DELIMITED SIZE
                     "/"                                DELIMITED SIZE
                     NSUBPRO-WS                         DELIMITED SIZE
                     " POR ESTAR COMO REFERENCIA "      DELIMITED SIZE
                                       INTO TEXTO-ARR(CTA-MSG)
           END-IF.

      *
       ELIMINA-SUBPROCESO-1.
      *--------------------
           EXEC SQL
           DELETE FROM  ASQRY01F WHERE WHFILE IN(SELECT ARCBAS
           FROM ASQRY09F WHERE NROPRO = :NUMPRO-WS AND
           CORPRO = :NSUBPRO-WS)
           END-EXEC

           EXEC SQL
           DELETE FROM  ASQRY09F WHERE NROPRO = :NUMPRO-WS AND
           CORPRO = :NSUBPRO-WS
           END-EXEC

           EXEC SQL
           DELETE FROM  ASQRY12F WHERE NROPRO = :NUMPRO-WS AND
           CORPRO = :NSUBPRO-WS
           END-EXEC

           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY09F WHERE NROPRO = :NUMPRO-WS
           END-EXEC

           EXEC SQL
             UPDATE  ASQRY10F SET CANSPR = :WS-CTA-NUMPRO WHERE
             NROPRO = :NUMPRO-WS
           END-EXEC

           ADD    1      TO CTA-MSG
           STRING "SE HA ELIMINADO PROCESO/SUBPROCESO " DELIMITED SIZE
                  NUMPRO-WS                          DELIMITED SIZE
                  "/"                                DELIMITED SIZE
                  NSUBPRO-WS                         DELIMITED SIZE
                                    INTO TEXTO-ARR(CTA-MSG).
      *
       GRABA-REGRABA-ASQRY09F.
      *----------------------
           CLOSE ASQRY09F
           OPEN I-O ASQRY09F

           MOVE NUMPRO-WS          TO NROPRO OF ASQRY09F
           MOVE CORPRO OF W01-II   TO CORPRO OF ASQRY09F
           READ ASQRY09F END-READ
           IF FS-ASQRY09F = "23"
              INITIALIZE RASQRY09F REPLACING
                         ALPHANUMERIC DATA BY SPACES
                         NUMERIC      DATA BY ZEROES
              MOVE CORR W01-I      TO RASQRY09F
              MOVE NUMPRO-WS       TO NROPRO OF ASQRY09F
              CALL   "MRTVDAF2" USING AAAAMMDD DDMMAAAA
              CANCEL "MRTVDAF2"
              ACCEPT WS-HHMMSSSS FROM TIME
              MOVE USUARIO-LNK     TO USRMOD OF ASQRY09F
                                      USRCRE OF ASQRY09F
              MOVE AAAAMMDD        TO FECMOD OF ASQRY09F
                                      FECCRE OF ASQRY09F
              MOVE WS-HORA         TO HORMOD OF ASQRY09F
                                      HORCRE OF ASQRY09F
              IF RARC OF W01-II = 1
                 MOVE "S"          TO REEARC OF ASQRY09F
              ELSE
                 MOVE "N"          TO REEARC OF ASQRY09F
              END-IF
              IF RJOB OF W01-II = 1
                 MOVE "S"          TO RETJOB OF ASQRY09F
              ELSE
                 MOVE "N"          TO RETJOB OF ASQRY09F
              END-IF
              IF AREG   OF W01-II = 1
                 MOVE "S"          TO ADDREG OF ASQRY09F
              ELSE
                 MOVE "N"          TO ADDREG OF ASQRY09F
              END-IF
              IF RUNICO OF W01-II = 1
                 MOVE "S"          TO REGUNI OF ASQRY09F
                 MOVE "DISTINCT"   TO SELECC OF ASQRY09F(8:8)
              ELSE
                 MOVE "N"          TO REGUNI OF ASQRY09F
                 MOVE "        "   TO SELECC OF ASQRY09F(8:8)
              END-IF
              WRITE R-ASQRY09F END-WRITE
           ELSE
           IF FS-ASQRY09F = "00"
              MOVE CORR W01-I      TO RASQRY09F
              CALL   "MRTVDAF2" USING AAAAMMDD DDMMAAAA
              CANCEL "MRTVDAF2"
              ACCEPT WS-HHMMSSSS FROM TIME
              MOVE USUARIO-LNK     TO USRMOD OF ASQRY09F
              MOVE AAAAMMDD        TO FECMOD OF ASQRY09F
              MOVE WS-HORA         TO HORMOD OF ASQRY09F
              IF RARC OF W01-II = 1
                 MOVE "S"          TO REEARC OF ASQRY09F
              ELSE
                 MOVE "N"          TO REEARC OF ASQRY09F
              END-IF
              IF RJOB OF W01-II = 1
                 MOVE "S"          TO RETJOB OF ASQRY09F
              ELSE
                 MOVE "N"          TO RETJOB OF ASQRY09F
              END-IF
              IF AREG   OF W01-II = 1
                 MOVE "S"          TO ADDREG OF ASQRY09F
              ELSE
                 MOVE "N"          TO ADDREG OF ASQRY09F
              END-IF
              IF RUNICO OF W01-II = 1
                 MOVE "S"          TO REGUNI OF ASQRY09F
                 MOVE "DISTINCT"   TO SELECC OF ASQRY09F(8:8)
              ELSE
                 MOVE "N"          TO REGUNI OF ASQRY09F
                 MOVE "        "   TO SELECC OF ASQRY09F(8:8)
              END-IF
              REWRITE R-ASQRY09F END-REWRITE.

           CLOSE      ASQRY09F
           OPEN INPUT ASQRY09F
      *
      *  SE ACTUALIZA CANTIDAD DE SUBPROCESOS
      *
           MOVE ZEROES     TO WS-CTA-NUMPRO
           EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC (5, 0)) INTO :WS-CTA-NUMPRO
           FROM  ASQRY09F WHERE NROPRO = :NUMPRO-WS
           END-EXEC

           EXEC SQL
             UPDATE  ASQRY10F SET CANSPR = :WS-CTA-NUMPRO WHERE
             NROPRO = :NUMPRO-WS
           END-EXEC.


      *
       AYUDA-PROCESO-SUBPRO.
      *----------------------
           MOVE 'SUBPROCESO'       TO NOMAYU-16C
           MOVE SPACES             TO SELECC-16C
           STRING '(DIGITS(NROPRO) '    DELIMITED SIZE
                  '|| DIGITS(CORPRO)) ' DELIMITED SIZE
                  ' <> "'               DELIMITED SIZE
                  NUMPRO-WS             DELIMITED SIZE
                  CORPRO  OF W01-II     DELIMITED SIZE
                  '" '                  DELIMITED SIZE
                  ' AND NROPRO = '      DELIMITED SIZE
                  NUMPRO-WS             DELIMITED SIZE
                                   INTO SELECC-16C
           MOVE SPACES             TO CODIGO-16C
           MOVE ZEROES             TO LARGOKEY-16C
                                      LARGODES-16C

           CALL   'ASQRY05' USING NOMAYU-16C
                                   SELECC-16C
                                   RETORNO-16C

           CANCEL 'ASQRY05'
           IF CODIGO-16C NOT = SPACES
              MOVE CODIGO-16C(1:5)  TO PROREF OF W01-I
              MOVE CODIGO-16C(7:5)  TO CPRREF OF W01-I.
      *
       AYUDA-SENTENCIA-SQL.
      *--------------------
           MOVE 'SENSQL    '       TO NOMAYU-16C
           MOVE SPACES             TO SELECC-16C
           MOVE SPACES             TO CODIGO-16C
           MOVE ZEROES             TO LARGOKEY-16C
                                      LARGODES-16C

           CALL   'ASQRY05' USING NOMAYU-16C
                                   SELECC-16C
                                   RETORNO-16C

           CANCEL 'ASQRY05'.
           IF CODIGO-16C NOT = SPACES
              MOVE CODIGO-16C(07:30)  TO WS-H-SENSQL
              MOVE POSCURSOR OF W02-II TO WS-H-PC
              DIVIDE WS-H-PC BY 70 GIVING WS-FILA REMAINDER
                                          WS-COLU
              MOVE WS-H-SENSQL    TO SENSQL01 OF W02-II(WS-H-PC:30)
              ADD  2              TO WS-FILA
              MOVE WS-FILA        TO FILA OF W02-II
              MOVE WS-COLU        TO COLU OF W02-II.
      *
       AYUDA-CAMPOS-FILE.
      *------------------
           MOVE 'CAMPOSFILE'       TO NOMAYU-16C
           MOVE SPACES             TO SELECC-16C
           STRING 'WHFILE '           DELIMITED SIZE
                  ARCHIVOS-SEL        DELIMITED SIZE
                                 INTO SELECC-16C
           MOVE SPACES             TO CODIGO-16C
           MOVE ZEROES             TO LARGOKEY-16C
                                      LARGODES-16C

           CALL   'ASQRY05' USING NOMAYU-16C
                                   SELECC-16C
                                   RETORNO-16C

           CANCEL 'ASQRY05'.
           IF CODIGO-16C NOT = SPACES
              MOVE SPACES             TO WS-H-ARC-CAMPO
              MOVE CODIGO-16C(01:10)  TO WS-H-ARCHIVO
              MOVE CODIGO-16C(12:10)  TO WS-H-CAMPO
              STRING WS-H-ARCHIVO DELIMITED BY " "
                     "."          DELIMITED BY SIZE
                     WS-H-CAMPO   DELIMITED BY " "
                               INTO WS-H-ARC-CAMPO
              MOVE POSCURSOR OF W02-II TO WS-H-PC
              DIVIDE WS-H-PC BY 70 GIVING WS-FILA REMAINDER
                                          WS-COLU
              MOVE WS-H-ARC-CAMPO TO SENSQL01 OF W02-II(WS-H-PC:21)
              ADD  2              TO WS-FILA
              MOVE WS-FILA        TO FILA OF W02-II
              MOVE WS-COLU        TO COLU OF W02-II.
      *
       CARGAR-WS00.
      *------------
           PERFORM LIMPIAR-WS00

           MOVE SPACES       TO SEL-NPROC
                                SEL-DPROC
                                SEL-C1
           IF NPROC OF WC00-II NOT = 0
              STRING ' WHERE NROPRO >= ' DELIMITED SIZE
                     NPROC OF WC00-II  DELIMITED SIZE
                                  INTO SEL-NPROC
           END-IF
           IF DPROC OF WC00-II NOT = SPACES
              IF NPROC OF WC00-II NOT = 0
                 STRING ' AND NOMPRO LIKE "%' DELIMITED SIZE
                        DPROC OF WC00-II  DELIMITED BY ' '
                        '%" '             DELIMITED SIZE
                                     INTO SEL-DPROC
              ELSE
                 STRING ' WHERE NOMPRO LIKE "%' DELIMITED SIZE
                        DPROC OF WC00-II  DELIMITED BY ' '
                        '%" '             DELIMITED SIZE
                                     INTO SEL-DPROC
              END-IF
           END-IF

           STRING 'SELECT NROPRO, NOMPRO, CANSPR ' DELIMITED SIZE
                  'FROM  ASQRY10F '         DELIMITED SIZE
                  SEL-NPROC                        DELIMITED SIZE
                  SEL-DPROC                        DELIMITED SIZE
                  ' ORDER BY NROPRO '              DELIMITED SIZE
                                              INTO SEL-C1
           EXEC SQL
                DECLARE SELC1 STATEMENT
           END-EXEC
           EXEC SQL
                PREPARE SELC1 FROM :SEL-C1
           END-EXEC
           EXEC SQL
                DECLARE C1 CURSOR FOR SELC1
           END-EXEC.

           PERFORM OPEN-CURSOR-C1
           PERFORM FETCH-CURSOR-C1
           PERFORM UNTIL SQLCODE NOT = 0
                 ADD 1   TO NREL01
                 MOVE NROPRO-C1      TO NROPRO OF WS00-OO
                 MOVE NOMPRO-C1      TO DESPRO OF WS00-OO
                 MOVE CANSPR-C1      TO CANSPR OF WS00-OO
                 MOVE ZEROES         TO CTL001 OF WS00-OO
                 MOVE SPACES         TO DETSFL OF WS00-OO
                 STRING "  "            DELIMITED SIZE
                        NROPRO-C1       DELIMITED SIZE
                        "    "          DELIMITED SIZE
                        NOMPRO-C1       DELIMITED SIZE
                                   INTO DETSFL OF WS00-OO
                 WRITE SUBFILE R-PANTALLA1 FROM WS00-OO FORMAT "WS00"
                       END-WRITE
                 PERFORM FETCH-CURSOR-C1

           END-PERFORM
           PERFORM CLOSE-CURSOR-C1
           IF NREL01 > 0
              MOVE I-ON         TO IN30 OF WC00-O-INDIC
                                   IN31 OF WC00-O-INDIC
              MOVE I-OFF        TO IN32 OF WC00-O-INDIC
           ELSE
              MOVE I-OFF        TO IN30 OF WC00-O-INDIC
              MOVE I-ON         TO IN31 OF WC00-O-INDIC
              MOVE I-OFF        TO IN32 OF WC00-O-INDIC.

      *
       CARGAR-WS03.
      *------------
           MOVE NUMPRO OF WC03-II    TO NUMPRO-WS
           PERFORM LIMPIAR-WS03.

           EXEC SQL
           DECLARE C2 CURSOR FOR SELECT CORPRO, DCOPRO FROM
            ASQRY09F WHERE NROPRO = :NUMPRO-WS ORDER BY CORPRO
           END-EXEC.

           PERFORM OPEN-CURSOR-C2
           PERFORM FETCH-CURSOR-C2
           PERFORM UNTIL SQLCODE NOT = 0
                 ADD 1   TO NREL04
                 MOVE CORPRO-C2      TO NSUBPRO OF WS03-OO
                 MOVE DCOPRO-C2      TO DSUBPRO OF WS03-OO
                 MOVE ZEROES         TO CTL001 OF WS03-OO
                 MOVE SPACES         TO DETSFL OF WS03-OO
                 STRING "    "          DELIMITED SIZE
                        CORPRO-C2       DELIMITED SIZE
                        "     "         DELIMITED SIZE
                        DCOPRO-C2       DELIMITED SIZE
                                   INTO DETSFL OF WS03-OO
                 WRITE SUBFILE R-PANTALLA4 FROM WS03-OO FORMAT "WS03"
                       END-WRITE
                 PERFORM FETCH-CURSOR-C2

           END-PERFORM
           PERFORM CLOSE-CURSOR-C2
           IF NREL04 > 0
              MOVE I-ON         TO IN30 OF WC03-O-INDIC
                                   IN31 OF WC03-O-INDIC
              MOVE I-OFF        TO IN32 OF WC03-O-INDIC
           ELSE
              MOVE I-OFF        TO IN30 OF WC03-O-INDIC
              MOVE I-ON         TO IN31 OF WC03-O-INDIC
              MOVE I-OFF        TO IN32 OF WC03-O-INDIC.

      *
       OPEN-CURSOR-C1.
      *---------------
           EXEC SQL
                OPEN C1
           END-EXEC.
      *
       OPEN-CURSOR-C2.
      *---------------
           EXEC SQL
                OPEN C2
           END-EXEC.
      *
       CLOSE-CURSOR-C1.
      *---------------
           EXEC SQL
                CLOSE C1
           END-EXEC.
      *
       CLOSE-CURSOR-C2.
      *---------------
           EXEC SQL
                CLOSE C2
           END-EXEC.
      *
       FETCH-CURSOR-C1.
      *---------------
           EXEC SQL
                FETCH NEXT FROM C1 INTO :DATOS-CURSOR-C1
           END-EXEC.
      *
       FETCH-CURSOR-C2.
      *---------------
           EXEC SQL
                FETCH NEXT FROM C2 INTO :DATOS-CURSOR-C2
           END-EXEC.
      *
       LIMPIAR-WS00.
      *-------------
           MOVE ZEROES          TO NREL01
           MOVE I-OFF           TO IN30 OF WC00-O-INDIC
                                   IN31 OF WC00-O-INDIC
           MOVE I-ON            TO IN32 OF WC00-O-INDIC
           WRITE R-PANTALLA1 FROM WC00-II FORMAT "WC00"
                             INDICATORS ARE WC00-O-INDIC END-WRITE.
      *
       LIMPIAR-WS01.
      *-------------
           MOVE ZEROES          TO NREL02
           MOVE I-OFF           TO IN30 OF WC01-O-INDIC
                                   IN31 OF WC01-O-INDIC
           MOVE I-ON            TO IN32 OF WC01-O-INDIC
           WRITE R-PANTALLA2 FROM WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-O-INDIC END-WRITE.
      *
       LIMPIAR-WS02.
      *-------------
           MOVE ZEROES          TO NREL03
           MOVE I-OFF           TO IN30 OF WC02-O-INDIC
                                   IN31 OF WC02-O-INDIC
           MOVE I-ON            TO IN32 OF WC02-O-INDIC
           WRITE R-PANTALLA3 FROM WC02-II FORMAT "WC02"
                             INDICATORS ARE WC02-O-INDIC END-WRITE.
      *
       LIMPIAR-WS03.
      *-------------
           MOVE ZEROES          TO NREL04
           MOVE I-OFF           TO IN30 OF WC03-O-INDIC
                                   IN31 OF WC03-O-INDIC
           MOVE I-ON            TO IN32 OF WC03-O-INDIC
           WRITE R-PANTALLA4 FROM WC03-II FORMAT "WC03"
                             INDICATORS ARE WC03-O-INDIC END-WRITE.

      *
       LIMPIAR-WS04.
      *-------------
           MOVE ZEROES          TO NREL07
           MOVE I-OFF           TO IN30 OF WC04-O-INDIC
                                   IN31 OF WC04-O-INDIC
           MOVE I-ON            TO IN32 OF WC04-O-INDIC
           WRITE R-PANTALLA7 FROM WC04-II FORMAT "WC04"
                             INDICATORS ARE WC04-O-INDIC END-WRITE.

       TERMINO.
      *--------
           CLOSE  ASQRY09F   PANTALLA1 PANTALLA2 PANTALLA3 PANTALLA4
                  PANTALLA5 PANTALLA6 PANTALLA7 PANTALLA8.
      *
       SELECCION-ARCHIVOS-UTILIZAR.
      *----------------------------

           MOVE SPACES       TO ARR-MAR-X

           EXEC SQL
           DECLARE ARCMARCAR CURSOR FOR SELECT CAST( NOMARC AS
           CHAR(125)) FROM ASQRY12F WHERE NROPRO = :NUMPRO-WS AND CORPRO
           = :NSUBPRO-WS
           END-EXEC.
           EXEC SQL OPEN ARCMARCAR END-EXEC
           EXEC SQL
              FETCH NEXT FROM ARCMARCAR FOR 240 ROWS INTO :ARR-MAR
           END-EXEC
           MOVE ARR-MAR-X          TO MARCAR-18C
           EXEC SQL CLOSE ARCMARCAR END-EXEC

           MOVE ZEROES             TO CANTIDAD-ARC-SEL
           MOVE SPACES             TO INCLUDE-ARC-SEL
           MOVE 'ARCQUERYS '       TO NOMAYU-18C
           MOVE SPACES             TO SELECC-18C
           MOVE SPACES             TO RETORNO-18C
           MOVE ZEROES             TO LARGOKEY-18C
                                      LARGODES-18C.

           CALL   'ASQRY07' USING NOMAYU-18C
                                   SELECC-18C
                                   LARGOKEY-18C
                                   LARGODES-18C
                                   RETORNO-18C
                                   MARCAR-18C.

           CANCEL 'ASQRY07'
           IF RETORNO-18C NOT = SPACES
           EXEC SQL
           DELETE FROM  ASQRY12F WHERE NROPRO = :NUMPRO-WS AND
           CORPRO = :NSUBPRO-WS
           END-EXEC
           END-IF
              MOVE RETORNO-18C TO ARR-SEL-X
              MOVE 1      TO I P1
              PERFORM REC-ARCHIVOS-SEL VARYING I FROM 1 BY 1 UNTIL
                      I > 240.

      *
       REC-ARCHIVOS-SEL.
      *-----------------
           IF CODIGO-X(I) NOT = SPACES
              IF CANTIDAD-ARC-SEL > 0
                 MOVE ', '            TO INCLUDE-ARC-SEL(P1:2)
                 ADD  1               TO P1
              END-IF
              ADD  1                  TO CANTIDAD-ARC-SEL
              MOVE CODIGO-X(I)        TO ARCHIVOS-USAR
              MOVE SPACES             TO ARCHIVO-12
              STRING '"'                 DELIMITED SIZE
                     ARCHIVOS-USAR       DELIMITED SIZE
                     '"'                 DELIMITED SIZE
                                     INTO ARCHIVO-12
              MOVE ARCHIVO-12        TO  INCLUDE-ARC-SEL(P1:12)

              EXEC SQL
           INSERT INTO  ASQRY12F VALUES(:NUMPRO-WS, :NSUBPRO-WS,
           :ARCHIVOS-USAR)
              END-EXEC
              ADD  12                TO  P1.
      *
       CONTINUA-SELECCION.
      *-------------------
           MOVE NUMPRO-WS          TO NROPRO OF ASQRY09F
           MOVE CORPRO OF W01-II   TO CORPRO OF ASQRY09F
           READ ASQRY09F END-READ
           IF FS-ASQRY09F = '00'
              PERFORM DESCOMPONE-SELECCION-CAMPOS
           END-IF

           MOVE SPACES                TO ARC-REFERENCIA
           IF PROREF OF W01-II NOT = ZEROES AND
              CPRREF OF W01-II NOT = ZEROES
              MOVE PROREF OF W01-II   TO NROPRO OF ASQRY09F
              MOVE CPRREF OF W01-II   TO CORPRO OF ASQRY09F
              READ ASQRY09F END-READ
              IF FS-ASQRY09F = '00'
                 MOVE ARCBAS OF ASQRY09F TO ARC-REFERENCIA
              END-IF
           END-IF

           MOVE SPACES    TO WSELECCION
           IF SEL-ARC = 'S'
           STRING '(WHFILE = "'                    DELIMITED SIZE
                  ARC-REFERENCIA                   DELIMITED SIZE
                  '" OR WHFILE IN('                DELIMITED SIZE
                  INCLUDE-ARC-SEL                  DELIMITED SIZE
                  '))'                             DELIMITED SIZE
                                              INTO WSELECCION
           ELSE
           STRING '(WHFILE = "'                    DELIMITED SIZE
                  ARC-REFERENCIA                   DELIMITED SIZE
                  '") '                            DELIMITED SIZE
                                              INTO WSELECCION
           END-IF

           STRING 'SELECT (WHFILE || " " '         DELIMITED SIZE
                  '|| WHFLDI || " " '              DELIMITED SIZE
                  ' || SUBSTR(WHFTXT, 01, 30)), '  DELIMITED SIZE
                  ' WHFLDB, WHFLDD, WHFLDP, '      DELIMITED SIZE
                  ' CASE WHFLDT WHEN "A" '         DELIMITED SIZE
                  ' THEN "CHARACTER        " '     DELIMITED SIZE
                  ' WHEN "S" THEN '                DELIMITED SIZE
                  ' "NUMERIC          " '          DELIMITED SIZE
                  ' WHEN "F" THEN '                DELIMITED SIZE
                  ' "FLOAT            " '          DELIMITED SIZE
                  ' WHEN "P" THEN '                DELIMITED SIZE
                  ' "DECIMAL          " '          DELIMITED SIZE
                  ' WHEN "L" THEN '                DELIMITED SIZE
                  ' "DATE             " '          DELIMITED SIZE
                  ' WHEN "T" THEN '                DELIMITED SIZE
                  ' "TIME             " '          DELIMITED SIZE
                  ' WHEN "G" THEN '                DELIMITED SIZE
                  ' "GRAPHIC          " END '      DELIMITED SIZE
                  ' FROM ASQRY01F WHERE '           DELIMITED SIZE
                  WSELECCION                       DELIMITED SIZE
                   ' '                             DELIMITED SIZE
                   INTO                            RECSEL

            EXEC SQL
              DECLARE SELECT_REC STATEMENT
            END-EXEC

            EXEC SQL
              PREPARE SELECT_REC FROM :RECSEL
            END-EXEC

            EXEC SQL
              DECLARE SELCUR CURSOR FOR SELECT_REC
            END-EXEC

            EXEC SQL OPEN SELCUR END-EXEC

            PERFORM LEER-CURSOR-SELCUR
            PERFORM LIMPIAR-WS01
            PERFORM UNTIL SQLCODE NOT = 0
                    PERFORM LLENA-SFL-WS01
                    PERFORM LEER-CURSOR-SELCUR
            END-PERFORM
            EXEC SQL CLOSE SELCUR END-EXEC

            IF NREL02 NOT = ZEROES
               MOVE I-ON       TO IN30 OF WC01-O-INDIC
                                  IN31 OF WC01-O-INDIC
               MOVE I-OFF      TO IN32 OF WC01-O-INDIC
            ELSE
               MOVE I-ON       TO IN31 OF WC01-O-INDIC
               MOVE I-OFF      TO IN30 OF WC01-O-INDIC
                                  IN32 OF WC01-O-INDIC
            END-IF

           WRITE R-PANTALLA5 FROM W01-II FORMAT 'W01'
                             INDICATORS ARE W01-O-INDIC END-WRITE

            MOVE 'N'           TO SALIR-CICLO
            MOVE '00'          TO MDTO
            PERFORM UNTIL F3 OR SALIR-CICLO = 'S'
                    MOVE 1             TO NREL02
                    PERFORM PROCESO-WC01
                    IF F9
                       PERFORM GENERA-ARCHIVO
                    END-IF
            END-PERFORM.
            IF F3
               MOVE ZEROES TO MDTO.

      *
       DESCOMPONE-SELECCION-CAMPOS.
      *----------------------------
            INITIALIZE CAMPOS-SEL-ARR
            MOVE 17     TO PINI-FLD
            MOVE 39     TO PINI-COM
            MOVE "N"    TO SALIR-FLD
            MOVE ZEROES TO P-S

            PERFORM UNTIL SALIR-FLD = "S"
              IF SELECC OF ASQRY09F(PINI-FLD:1) NOT = SPACES
                 IF SELECC OF ASQRY09F(PINI-COM:1) = ","
                 OR SELECC OF ASQRY09F((PINI-COM + 5):4) = "FROM"
                    MOVE SELECC OF ASQRY09F(PINI-FLD:22) TO ARCFLD
                    UNSTRING ARCFLD  DELIMITED BY "."
                             INTO ARCHIVO-SEL,
                                  CAMPO-SEL
                    ADD  1               TO P-S
                    MOVE ARCHIVO-SEL     TO ARCHIVO-S(P-S)
                    MOVE CAMPO-SEL       TO CAMPO-S(P-S)
                    IF SELECC OF ASQRY09F((PINI-FLD + 24):1) = SPACES
                       ADD 24            TO PINI-FLD
                                            PINI-COM
                    ELSE
                       IF SELECC OF ASQRY09F((PINI-COM + 24):1) = ","
                       OR SELECC OF ASQRY09F((PINI-COM + 29):4) = "FROM"
                          ADD 24         TO PINI-COM
                                            PINI-FLD
                       ELSE
                          ADD 702         TO PINI-FLD
                                             PINI-COM
                       END-IF
                    END-IF
                 ELSE
                    ADD 702              TO PINI-FLD
                                            PINI-COM
                 END-IF
              ELSE
                 MOVE "S"                               TO SALIR-FLD
              END-IF
      *       ADD 24       TO PINI-FLD
            END-PERFORM.
      *
       GENERA-ARCHIVO.
      *---------------
            PERFORM CARGAR-WS02
            MOVE I-OFF         TO IN12 OF WC02-I-INDIC
                                  IN70 OF WC02-O-INDIC
            MOVE 1             TO NREL02


           MOVE ARCBAS OF W01-II  TO ARCHIVO    OF WC02-II
           MOVE BIBGEN OF W01-II  TO BIBLIOTECA OF WC02-II
           PERFORM UNTIL F12 OR SALIR-CICLO = "S"
                   PERFORM PROCESO-WC02
                   MOVE I-OFF TO IN70 OF WC02-O-INDIC
                   IF F9
                      PERFORM GENERA-ARCHIVO-FINAL
                   ELSE
                     IF F13
                        PERFORM SECUENCIAR-ORDEN
                     ELSE
                       IF F15
                          MOVE "N"       TO EXISTE-CAMPO
                          PERFORM REVISA-ORDEN
                          IF EXISTE-CAMPO = "N"
                             PERFORM MOSTRAR-PEPAS
                             PERFORM VOLVER-CARGAR-WS02
                          END-IF
                        END-IF
                     END-IF
                   END-IF
                   IF CTA-MSG NOT = ZEROES
                      CALL    "VISERR2" USING VARIABLE-3250
                      CANCEL  "VISERR2"
                      MOVE    SPACES        TO VARIABLE-3250
                      MOVE    ZEROES        TO CTA-MSG
                   END-IF
           END-PERFORM
           IF F12
              WRITE R-PANTALLA5 FROM W01-II FORMAT "W01"
                    INDICATORS ARE W01-O-INDIC END-WRITE
              IF CTA-MSG NOT = ZEROES
                 CALL    "VISERR2" USING VARIABLE-3250
                 CANCEL  "VISERR2"
                 MOVE    SPACES        TO VARIABLE-3250
                 MOVE    ZEROES        TO CTA-MSG
              END-IF
              GO TO CONTINUA-SELECCION.
      *
       CARGAR-WS02.
      *------------
            PERFORM LIMPIAR-WS02
            PERFORM LIMPIA-ARR-SA
            MOVE ZEROES  TO NREL03
            MOVE "N"     TO SEL-NUMRUT
            MOVE ZEROES  TO POS-ARR-SA
            PERFORM LEER-MODIFICADO-WS01
            PERFORM VARYING P-WS02 FROM 1 BY 1 UNTIL P-WS02 > 999
                MOVE SPACES   TO ARCHIVO-WS02  (P-WS02)
                                 CAMPO-WS02    (P-WS02)
                                 DCAMPO-WS02   (P-WS02)
                MOVE ZEROES   TO ORDEN-WS02    (P-WS02)
                MOVE SPACES   TO FLDREF-WS02   (P-WS02)
                MOVE ZEROES   TO LARGO-WS02    (P-WS02)
                                 DIGIT-WS02
                                 DECIM-WS02
                MOVE SPACES   TO TIPO-WS02     (P-WS02)
            END-PERFORM

            PERFORM UNTIL FS-PAN02 NOT = "00"
                    PERFORM LLENA-SFL-WS02

                    MOVE ZEROES   TO CTL001 OF WS01-OO
                    REWRITE SUBFILE R-PANTALLA2 FROM WS01-OO
                                    FORMAT         "WS01"
                    END-REWRITE
                    PERFORM LEER-MODIFICADO-WS01
            END-PERFORM
            PERFORM RESCATA-PEPAS
            IF NREL03 NOT = ZEROES
               MOVE I-ON       TO IN30 OF WC02-O-INDIC
                                  IN31 OF WC02-O-INDIC
               MOVE I-OFF      TO IN32 OF WC02-O-INDIC
            ELSE
               MOVE I-ON       TO IN31 OF WC02-O-INDIC
               MOVE I-OFF      TO IN30 OF WC02-O-INDIC
                                  IN32 OF WC02-O-INDIC
            END-IF.
      *
       VOLVER-CARGAR-WS02.
      *-------------------
            PERFORM LIMPIAR-WS02
            PERFORM VARYING P-WS02 FROM 1 BY 1 UNTIL
                    CAMPO-WS02(P-WS02) = SPACES
              ADD  1                           TO NREL03
              MOVE ARCHIVO-WS02(P-WS02)        TO ARCHIVO  OF WS02-OO
                                                  ARCCAMPO OF WS02-OO
              MOVE CAMPO-WS02  (P-WS02)        TO CAMPO    OF WS02-OO
              MOVE DCAMPO-WS02 (P-WS02)        TO DCAMPO   OF WS02-OO
              MOVE ORDEN-WS02  (P-WS02)        TO ORDEN    OF WS02-OO
              MOVE FLDREF-WS02 (P-WS02)        TO FLDREF   OF WS02-OO
              MOVE LARGO-WS02  (P-WS02)        TO LARGO    OF WS02-OO
              MOVE DIGIT-WS02  (P-WS02)        TO DIGIT    OF WS02-OO
              MOVE DECIM-WS02  (P-WS02)        TO DECIM    OF WS02-OO
              MOVE TIPO-WS02   (P-WS02)        TO TIPO     OF WS02-OO
              IF CAMPO OF WS02-OO(1:4) = "PEPA"
                 MOVE I-ON                     TO IN80 OF WS02-O-INDIC
              ELSE
                 MOVE I-OFF                    TO IN80 OF WS02-O-INDIC
              END-IF
              WRITE SUBFILE R-PANTALLA3 FROM WS02-OO FORMAT "WS02"
                            INDICATORS ARE WS02-O-INDIC END-WRITE
            END-PERFORM.
            PERFORM RESCATA-PEPAS
            IF NREL03 NOT = ZEROES
               MOVE I-ON       TO IN30 OF WC02-O-INDIC
                                  IN31 OF WC02-O-INDIC
               MOVE I-OFF      TO IN32 OF WC02-O-INDIC
            ELSE
               MOVE I-ON       TO IN31 OF WC02-O-INDIC
               MOVE I-OFF      TO IN30 OF WC02-O-INDIC
                                  IN32 OF WC02-O-INDIC
            END-IF.
      *
       RESCATA-PEPAS.
      *--------------
           EXEC SQL
           DECLARE CPEPAS CURSOR FOR SELECT NROPEP, WHFTXT, WHFLDB,
           WHFLDD, WHFLDP, WHFLDT FROM ASQRY18F WHERE NROPRO =
           :NUMPRO-WS AND CORPRO = :NSUBPRO-WS ORDER BY NROPEP
           END-EXEC
           EXEC SQL OPEN CPEPAS END-EXEC
           MOVE 900      TO ORDEN-PEPA
           PERFORM FETCH-CPEPAS
           PERFORM UNTIL SQLCODE NOT = 0
              MOVE SPACES                      TO ARCHIVO  OF WS02-OO
                                                  ARCCAMPO OF WS02-OO
              MOVE SPACES                      TO CAMPO    OF WS02-OO
              STRING "PEPA"                    DELIMITED SIZE
                     NROPEP-PEPA1              DELIMITED SIZE
                                             INTO CAMPO    OF WS02-OO
              MOVE DESPEP-PEPA1       (01: 28) TO DCAMPO   OF WS02-OO
              MOVE ORDEN-PEPA                  TO ORDEN    OF WS02-OO
              MOVE SPACES                      TO FLDREF   OF WS02-OO
              STRING "."                       DELIMITED SIZE
                    CAMPO OF WS02-OO           DELIMITED SIZE
                                           INTO FLDREF     OF WS02-OO
              MOVE WHFLDB-PEPA1                TO LARGO    OF WS02-OO
              MOVE WHFLDB-PEPA1                TO DIGIT    OF WS02-OO
              MOVE WHFLDP-PEPA1                TO DECIM    OF WS02-OO
              IF WHFLDT-PEPA1 = "S"
                 MOVE "NUMERIC"                TO TIPO     OF WS02-OO
              ELSE
                 MOVE "CHARACTER"              TO TIPO     OF WS02-OO
              END-IF
              ADD  1                           TO NREL03 ORDEN-PEPA
              MOVE I-ON                        TO IN80 OF WS02-O-INDIC
              WRITE SUBFILE R-PANTALLA3 FROM WS02-OO FORMAT "WS02"
                            INDICATORS ARE WS02-O-INDIC END-WRITE
              PERFORM FETCH-CPEPAS

           END-PERFORM.
           EXEC SQL CLOSE CPEPAS END-EXEC.

      *
       FETCH-CPEPAS.
      *-------------
           EXEC SQL
                FETCH NEXT FROM CPEPAS INTO :DATOS-CURSOR-CPEPA
           END-EXEC.
      *
       MOSTRAR-PEPAS.
      *--------------
           PERFORM CARGAR-WS04
           MOVE "  "    TO MDTO
           MOVE WS-DATECUR        TO DATECUR OF WC04-II
           MOVE DESPRO OF WS00-OO TO NOMPRO  OF WC04-II
           PERFORM PROCESO-WC04 UNTIL F12
           MOVE "  "    TO MDTO.

      *
       CARGAR-WS04.
      *------------
           PERFORM LIMPIAR-WS04
           EXEC SQL
             DECLARE CURPEPAS CURSOR FOR SELECT NROPEP, WHFTXT FROM
             ASQRY18F WHERE NROPRO = :NUMPRO-WS AND CORPRO = :NSUBPRO-WS
           END-EXEC.
           EXEC SQL OPEN CURPEPAS END-EXEC
           PERFORM LEER-FETCH-PEPAS
           PERFORM UNTIL SQLCODE NOT = 0
               ADD  1            TO NREL07
               MOVE SPACES       TO DETSFL OF WS04-OO
               STRING NROPEP-PEPA   DELIMITED SIZE
                      "     "       DELIMITED SIZE
                      DESPEP-PEPA   DELIMITED SIZE
                               INTO DETSFL OF WS04-OO
                  MOVE ZEROES      TO CTL001 OF WS04-OO
                  MOVE NROPEP-PEPA TO NROPEPA OF WS04-OO
                  WRITE SUBFILE R-PANTALLA7 FROM WS04-OO FORMAT "WS04"
                        END-WRITE
               PERFORM LEER-FETCH-PEPAS
           END-PERFORM
           EXEC SQL CLOSE CURPEPAS END-EXEC.
           IF NREL07 > 0
              MOVE I-ON   TO IN30 OF WC04-O-INDIC
                             IN31 OF WC04-O-INDIC
              MOVE I-OFF  TO IN32 OF WC04-O-INDIC
           ELSE
              MOVE I-OFF  TO IN30 OF WC04-O-INDIC
              MOVE I-ON   TO IN31 OF WC04-O-INDIC
              MOVE I-OFF  TO IN32 OF WC04-O-INDIC.

      *
       LEER-FETCH-PEPAS.
      *------------------
           EXEC SQL
                FETCH NEXT FROM CURPEPAS INTO :DATOS-CURSOR-PEPA
           END-EXEC.
      *
      *
       PROCESO-WC04.
      *-------------
           MOVE NUMPRO-WS     TO NUMPRO  OF WC04-II
           MOVE NSUBPRO-WS    TO NSUBPRO OF WC04-II
           WRITE R-PANTALLA7 FROM WC04-II FORMAT "WC04"
                             INDICATORS ARE WC04-O-INDIC END-WRITE
           READ  PANTALLA7   INTO WC04-II FORMAT "WC04"
                             INDICATORS ARE WC04-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG
           IF F9
              PERFORM INSERTA-PEPA
           ELSE
           IF F7
              PERFORM MODIFICA-PEPA
           ELSE
           IF F23
              PERFORM ELIMINA-PEPA.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
           PERFORM CARGAR-WS04.
      *
       INSERTA-PEPA.
      *-------------
           MOVE ZEROES   TO U-PEPA
           EXEC SQL
           SELECT MAX( NROPEP ) INTO :U-PEPA FROM ASQRY18F WHERE NROPRO
           = :NUMPRO-WS AND CORPRO = :NSUBPRO-WS
           END-EXEC.

           ADD  1                 TO U-PEPA.
           INITIALIZE W03-I REPLACING ALPHANUMERIC DATA BY SPACES
                                           NUMERIC DATA BY ZEROES.
           MOVE U-PEPA            TO NROPEPA OF W03-II
           MOVE 1                 TO TIPPEPA OF W03-II.
           MOVE "I"               TO FUNCION-WS
           PERFORM PROCESA-W03 UNTIL F3
           MOVE "  "   TO MDTO.

      *
       MODIFICA-PEPA.
      *---------------
           INITIALIZE W03-I REPLACING ALPHANUMERIC DATA BY SPACES
                                           NUMERIC DATA BY ZEROES.
           READ SUBFILE PANTALLA7 NEXT MODIFIED INTO WS04-OO
                                       FORMAT "WS04" END-READ
           IF FS-PAN07 = "00"
              OPEN INPUT ASQRY18F
              MOVE NUMPRO  OF WC04-II TO NROPRO OF ASQRY18F
              MOVE NSUBPRO OF WC04-II TO CORPRO OF ASQRY18F
              MOVE NROPEPA OF WS04-OO TO NROPEP OF ASQRY18F
              READ ASQRY18F END-READ
              IF FS-ASQRY18F = "00"
                 MOVE NROPEPA OF WS04-OO TO NROPEPA OF W03-II
                 MOVE WHFLDB  OF ASQRY18F TO LARPEPA OF W03-II
                 MOVE WHFLDP  OF ASQRY18F TO DECPEPA OF W03-II
                 MOVE WHFTXT  OF ASQRY18F TO DESPEPA OF W03-II
                 IF WHFLDT OF ASQRY18F = "S"
                    MOVE 1              TO TIPPEPA OF W03-II
                 ELSE
                    MOVE 2              TO TIPPEPA OF W03-II
                 END-IF
                 MOVE VALPEP OF ASQRY18F TO SENPEPA OF W03-II
                 MOVE "M"               TO FUNCION-WS
                 CLOSE ASQRY18F
                 PERFORM PROCESA-W03 UNTIL F3
                 MOVE "  "   TO MDTO.

      *
       ELIMINA-PEPA.
      *---------------
           READ SUBFILE PANTALLA7 NEXT MODIFIED INTO WS04-OO
                                       FORMAT "WS04" END-READ
           IF FS-PAN07 = "00"
              OPEN I-O   ASQRY18F
              MOVE NUMPRO  OF WC04-II TO NROPRO OF ASQRY18F
              MOVE NSUBPRO OF WC04-II TO CORPRO OF ASQRY18F
              MOVE NROPEPA OF WS04-OO TO NROPEP OF ASQRY18F
              READ ASQRY18F END-READ
              IF FS-ASQRY18F = "00"
                 DELETE ASQRY18F END-DELETE.
      *
       PROCESA-W03.
      *------------
           WRITE R-PANTALLA8 FROM W03-II FORMAT "W03"
                             END-WRITE
           READ  PANTALLA8   INTO W03-II FORMAT "W03"
                             INDICATORS ARE W03-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG

           IF F4
              PERFORM AYUDA-SENTENCIA-SQL-W03
           ELSE
           IF F6
              PERFORM AYUDA-CAMPOS-FILE-W03
           ELSE
           IF F9
              PERFORM VALIDA-W03
              IF CTA-MSG = 0
                 PERFORM ACTUALIZA-PEPA.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       AYUDA-SENTENCIA-SQL-W03.
      *------------------------
           MOVE 'SENSQL    '       TO NOMAYU-16C
           MOVE SPACES             TO SELECC-16C
           MOVE SPACES             TO CODIGO-16C
           MOVE ZEROES             TO LARGOKEY-16C
                                      LARGODES-16C

           CALL   'ASQRY05' USING NOMAYU-16C
                                   SELECC-16C
                                   RETORNO-16C

           CANCEL 'ASQRY05'.
           IF CODIGO-16C NOT = SPACES
              MOVE CODIGO-16C(07:30)  TO WS-H-SENSQL
              MOVE POSCURSOR OF W03-II TO WS-H-PC
              DIVIDE WS-H-PC BY 70 GIVING WS-FILA REMAINDER
                                          WS-COLU
              MOVE WS-H-SENSQL    TO SENPEPA  OF W03-II(WS-H-PC:30)
              ADD  2              TO WS-FILA
              MOVE WS-FILA        TO FILA OF W03-II
              MOVE WS-COLU        TO COLU OF W03-II.
      *
       AYUDA-CAMPOS-FILE-W03.
      *---------------------
           MOVE 'CAMPOSFILE'       TO NOMAYU-16C
           MOVE SPACES             TO SELECC-16C
           STRING 'WHFILE '           DELIMITED SIZE
                  ARCHIVOS-SEL        DELIMITED SIZE
                                 INTO SELECC-16C
           MOVE SPACES             TO CODIGO-16C
           MOVE ZEROES             TO LARGOKEY-16C
                                      LARGODES-16C

           CALL   'ASQRY05' USING NOMAYU-16C
                                   SELECC-16C
                                   RETORNO-16C

           CANCEL 'ASQRY05'.
           IF CODIGO-16C NOT = SPACES
              MOVE SPACES             TO WS-H-ARC-CAMPO
              MOVE CODIGO-16C(01:10)  TO WS-H-ARCHIVO
              MOVE CODIGO-16C(12:10)  TO WS-H-CAMPO
              STRING WS-H-ARCHIVO DELIMITED BY " "
                     "."          DELIMITED BY SIZE
                     WS-H-CAMPO   DELIMITED BY " "
                               INTO WS-H-ARC-CAMPO
              MOVE POSCURSOR OF W03-II TO WS-H-PC
              DIVIDE WS-H-PC BY 70 GIVING WS-FILA REMAINDER
                                          WS-COLU
              MOVE WS-H-ARC-CAMPO TO SENPEPA  OF W03-II(WS-H-PC:21)
              ADD  2              TO WS-FILA
              MOVE WS-FILA        TO FILA OF W03-II
              MOVE WS-COLU        TO COLU OF W03-II.
      *
       VALIDA-W03.
      *-----------
           MOVE SPACES             TO SELECCION-PEPA
           MOVE SENPEPA OF W03-II  TO SELECCION-PEPA

            IF SELECCION-PEPA = " "
               ADD  1           TO CTA-MSG
               MOVE "DEBE POSEER FILTRO PARA PEPA"
                    TO TEXTO-ARR(CTA-MSG)
            END-IF
            PERFORM VARYING POS-SQL FROM 1 BY 1 UNTIL POS-SQL > 700
                    OR SALIR-SQL = "S"
               IF SELECCION-PEPA(POS-SQL:1) = "'"
                  ADD  1           TO CTA-MSG
                MOVE "DEBE SELECCION COMILLAS DOBLES EN VEZ DE SIMPLES"
                       TO TEXTO-ARR(CTA-MSG)
               END-IF
            END-PERFORM.
            IF TIPPEPA OF W03-II < 1
               ADD  1           TO CTA-MSG
               MOVE "DEBE SELECCIONAR TIPO DE CAMPO                  "
                    TO TEXTO-ARR(CTA-MSG)
            END-IF
            IF TIPPEPA OF W03-II = 1
               IF (LARPEPA OF W03-II + DECPEPA OF W03-II) < 1 OR > 18
                  ADD  1           TO CTA-MSG
                MOVE "LARGO PARA CAMPO NUMERICO DEBE ESTAR ENTRE 1-18 "
                       TO TEXTO-ARR(CTA-MSG).
      *
       ACTUALIZA-PEPA.
      *---------------
            IF FUNCION-WS = "I"
               PERFORM GRABAR-ASQRY18F
            END-IF.
            IF FUNCION-WS = "M"
               PERFORM MODIFICA-ASQRY18F
            END-IF.
      *
       GRABAR-ASQRY18F.
      *---------------
            OPEN I-O ASQRY18F.
            MOVE NUMPRO-WS                   TO NROPRO   OF ASQRY18F
            MOVE NSUBPRO-WS                  TO CORPRO   OF ASQRY18F
            MOVE NROPEPA OF W03-II           TO NROPEP   OF ASQRY18F
            MOVE DESPEPA OF W03-II           TO WHFTXT   OF ASQRY18F
            IF TIPPEPA OF W03-II = 1
               MOVE "S"                      TO WHFLDT   OF ASQRY18F
               MOVE LARPEPA OF W03-II        TO WHFLDB   OF ASQRY18F
                                                WHFLDD   OF ASQRY18F
               MOVE DECPEPA OF W03-II        TO WHFLDP   OF ASQRY18F
            ELSE
               MOVE "A"                      TO WHFLDT   OF ASQRY18F
               MOVE LARPEPA OF W03-II        TO WHFLDB   OF ASQRY18F
               MOVE ZEROES                   TO WHFLDD   OF ASQRY18F
                                                WHFLDP   OF ASQRY18F
            END-IF
            MOVE SENPEPA OF W03-II           TO VALPEP   OF ASQRY18F
            WRITE R-ASQRY18F END-WRITE.

            CLOSE ASQRY18F.
      *
       MODIFICA-ASQRY18F.
      *---------------
            OPEN I-O ASQRY18F.
            MOVE NUMPRO-WS                   TO NROPRO   OF ASQRY18F
            MOVE NSUBPRO-WS                  TO CORPRO   OF ASQRY18F
            MOVE NROPEPA OF W03-II           TO NROPEP   OF ASQRY18F
            READ ASQRY18F END-READ
            IF FS-ASQRY18F = "00"
               MOVE DESPEPA OF W03-II           TO WHFTXT OF ASQRY18F
               IF TIPPEPA OF W03-II = 1
                  MOVE "S"                      TO WHFLDT   OF ASQRY18F
                  MOVE LARPEPA OF W03-II        TO WHFLDB   OF ASQRY18F
                                                   WHFLDD   OF ASQRY18F
                  MOVE DECPEPA OF W03-II        TO WHFLDP   OF ASQRY18F
               ELSE
                  MOVE "A"                      TO WHFLDT   OF ASQRY18F
                  MOVE LARPEPA OF W03-II        TO WHFLDB   OF ASQRY18F
                  MOVE ZEROES                   TO WHFLDD   OF ASQRY18F
                                                   WHFLDP   OF ASQRY18F
               END-IF
               MOVE SENPEPA OF W03-II           TO VALPEP   OF ASQRY18F
               REWRITE R-ASQRY18F END-REWRITE.

            CLOSE ASQRY18F.
      *
       GENERA-ARCHIVO-FINAL.
      *---------------------
            MOVE "N"       TO EXISTE-CAMPO
            PERFORM REVISA-ORDEN
            IF EXISTE-CAMPO = "S"
               ADD  1      TO CTA-MSG
               MOVE "NO PUEDEN EXISTIR 2 CAMPOS CON EL MISMO NOMBRE"
                    TO TEXTO-ARR(CTA-MSG)
            ELSE
            IF SAL-WS = "N" AND ORDEN-MALO = "N"
               PERFORM ARMA-CREATE-SQL
               MOVE I-ON   TO IN12 OF WC02-I-INDIC
               MOVE "S"    TO SALIR-CICLO.

      *
       SECUENCIAR-ORDEN.
      *-----------------
           MOVE 1                  TO NREL03
           PERFORM LEER-NEXT-WS03
           PERFORM UNTIL FS-PAN03 NOT = "00"
               MOVE NREL03    TO ORDEN OF WS02-OO
               REWRITE SUBFILE R-PANTALLA3 FROM WS02-OO FORMAT "WS02"
                       END-REWRITE
               ADD 1   TO NREL03
               PERFORM LEER-NEXT-WS03
           END-PERFORM.
      *
       LEER-NEXT-WS03.
      *---------------
           READ SUBFILE PANTALLA3 INTO WS02-OO FORMAT "WS02" END-READ.
      *
       ARMA-CREATE-SQL.
      *----------------
           MOVE ARCHIVO OF WC02-II    TO WS-ARCHIVO
                                         NOMARC-10A
           MOVE BIBLIOTECA OF WC02-II TO WS-BIBLIOTECA
                                         BIBARC-10A

           EXEC SQL
           DELETE FROM  ASQRY17F WHERE WHFILE = :NOMARC-10A
           END-EXEC
      *
      *  SE BORRA TABLA PARA PODER CREAR NUEVA
      *
           CALL   "ASQRY03P" USING NOMARC-10A BIBARC-10A
           CANCEL "ASQRY03P"
      *
      * SE INSERTAN CAMPOS DE ARCHIVO GENERADO EN ASQRY01F
      *
           CALL   "ASQRY03" USING PARAM
           CANCEL "ASQRY03"

           MOVE SPACES      TO     TEXTO-10A
           MOVE "2"         TO     OPCION-10A
           MOVE "1"         TO     RET-10A
           MOVE SPACES      TO     MBR-10A
           CALL  "ASQRY01P" USING  NOMARC-10A
                                   BIBARC-10A
                                   TEXTO-10A
                                   OPCION-10A
                                   RET-10A
                                   MBR-10A
           CANCEL "ASQRY01P"

           IF TEXTO-10A = "ASQRY17F"
              MOVE "12" TO    MDTO
              ADD   1   TO    CTA-MSG
              MOVE "NO SE PUEDE CONTINUAR, INTENTE MAS TARDE." TO
                   TEXTO-ARR(CTA-MSG)
           ELSE
              PERFORM CONT-ARMA-CREATE-SQL.

      *
       CONT-ARMA-CREATE-SQL.
      *---------------------
      *
      * SE LIMITA A 1.000.000 LA CANTIDAD DE REGISTROS
      *
           MOVE SPACES      TO     TEXTO-10A
           MOVE "3"         TO     OPCION-10A
           MOVE "1"         TO     RET-10A
           CALL  "ASQRY01P" USING  NOMARC-10A
                                   BIBARC-10A
                                   TEXTO-10A
                                   OPCION-10A
                                   RET-10A
                                   MBR-10A
           CANCEL "ASQRY01P"

           EXEC SQL
           DELETE FROM  ASQRY01F WHERE WHFILE = :NOMARC-10A
           END-EXEC

           PERFORM ALTERA-ASQRY17F

           EXEC SQL
           INSERT INTO  ASQRY01F SELECT WHFILE, WHFLDI, WHFLDB,
           WHFLDD, WHFLDP, WHFTXT, WHFLDT FROM  ASQRY17F WHERE
           WHFILE = :NOMARC-10A AND WHLIB = :BIBARC-10A
           END-EXEC.

           PERFORM DEL-ALTERA-ASQRY17F

      *
      * BORRA MIEMBRO CREADO
      *
           MOVE SPACES      TO     TEXTO-10A
           MOVE "4"         TO     OPCION-10A
           MOVE "1"         TO     RET-10A
           CALL  "ASQRY01P" USING  NOMARC-10A
                                   BIBARC-10A
                                   TEXTO-10A
                                   OPCION-10A
                                   RET-10A
                                   MBR-10A
           CANCEL "ASQRY01P"

           MOVE ZEROES     TO MDTO
           MOVE CONSQL-WS  TO SENSQL01 OF W02-II
           PERFORM PROCESO-W02 UNTIL F9 OR F3
           MOVE ZEROES    TO MDTO.
      *
       ALTERA-ASQRY17F.
      *---------------
           MOVE SPACES TO MANDATO-200
           STRING "OVRDBF FILE("       DELIMITED BY SIZE
                  "ASQRY17F"            DELIMITED BY SIZE
                  ") TOFILE("          DELIMITED BY SIZE
      *           "CPCRBUGD"           DELIMITED BY " "
      *           "/"                  DELIMITED BY SIZE
                  "ASQRY17F"            DELIMITED BY " "
                  ") MBR("             DELIMITED BY SIZE
                  MBR-10A              DELIMITED BY " "
                  ") SHARE(*YES) "     DELIMITED BY SIZE
                                  INTO MANDATO-200
           CALL  "QCMDEXC" USING MANDATO-200 LARGO-200.

      *
       DEL-ALTERA-ASQRY17F.
      *-------------------
           MOVE SPACES TO MANDATO-200
           STRING "DLTOVR FILE("       DELIMITED BY SIZE
                  "ASQRY17F"            DELIMITED BY SIZE
                  ") "                 DELIMITED BY SIZE
                                  INTO MANDATO-200
           CALL  "QCMDEXC" USING MANDATO-200 LARGO-200.

      *
       REVISA-ORDEN.
      *-------------
            PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 999
                    MOVE ZEROES TO NRO-VECES(Y)
                                   LARGO-ARR(Y)
                                   DIGIT-ARR(Y)
                                   DECIM-ARR(Y)
                    MOVE SPACES TO CAMPO-LARGO-ARR(Y)
                                   CAMPO-CORTO-ARR(Y)
                                   CAMPO-ARR      (Y)
                                   CAMPO-ARR-CON  (Y)
                                   TIPO-ARR       (Y)
            END-PERFORM

            MOVE    1        TO NREL03
            MOVE    ZEROES   TO POS-ARR-FILE-T
                                POS-ARR-CAMPO-T
            MOVE    SPACES   TO SELECCION-FINAL
                                SELECCION-FINAL-CON
            MOVE 'SELECT          '   TO SELECCION-FINAL(1:16)
            MOVE 'SELECT CAST((' TO SELECCION-FINAL-CON(1:13)
            MOVE 17          TO POS-INI-SEL
            MOVE 14          TO POS-INI-SEL-F
            MOVE ZEROES      TO LARGO-REGISTRO
            MOVE 'N'         TO EXISTE-CAMPO ORDEN-MALO

            PERFORM LEER-SFL-NEXT-WS02
            PERFORM UNTIL FS-PAN03 NOT = '00' OR EXISTE-CAMPO = 'S'
                                              OR ORDEN-MALO = 'S'
              IF ORDEN OF WS02-OO < 1
                 ADD  1      TO CTA-MSG
                 MOVE 'ORDEN DEBE SER MAYOR QUE 0 '
                       TO TEXTO-ARR(CTA-MSG)
                 MOVE 'S'    TO ORDEN-MALO
              ELSE
                 ADD 1        TO NRO-VECES(ORDEN OF WS02-OO)

                 MOVE CAMPO OF WS02-OO TO
                      CAMPO-LARGO-ARR(ORDEN OF WS02-OO)

                 MOVE CAMPO OF WS02-OO TO
                      CAMPO-CORTO-ARR(ORDEN OF WS02-OO)

                 MOVE FLDREF OF WS02-OO  TO
                                  CAMPO-REFER-ARR(ORDEN OF WS02-OO)

                 UNSTRING FLDREF OF WS02-OO DELIMITED BY '.'
                          INTO WS-ARCSEL
                               WS-CAMSEL

               MOVE LARGO  OF WS02-OO  TO LARGO-ARR(ORDEN OF WS02-OO)
               MOVE DIGIT  OF WS02-OO  TO DIGIT-ARR(ORDEN OF WS02-OO)
               MOVE DECIM  OF WS02-OO  TO DECIM-ARR(ORDEN OF WS02-OO)
               MOVE TIPO   OF WS02-OO  TO TIPO-ARR (ORDEN OF WS02-OO)
               MOVE DCAMPO OF WS02-OO  TO DESCR-ARR(ORDEN OF WS02-OO)

               IF WS-ARCSEL NOT = SPACES
                  PERFORM CHEQUEA-FILE-ARR
               END-IF
               PERFORM CHEQUEA-CAMPOS-ARR
              END-IF

               ADD 1        TO NREL03
               PERFORM LEER-SFL-NEXT-WS02
            END-PERFORM

            MOVE ZEROES     TO CTA-CAMPO-SEL
            PERFORM VARYING POS-ARR-CAMPO FROM 1 BY 1 UNTIL
                            POS-ARR-CAMPO > 500 OR EXISTE-CAMPO = 'S'
                                                OR ORDEN-MALO = 'S'
              IF CAMPO-ARR(POS-ARR-CAMPO) NOT = ' '
                 IF CTA-CAMPO-SEL > 0
                    MOVE ', '      TO SELECCION-FINAL(POS-INI-SEL: 2)
                    ADD  2         TO POS-INI-SEL

                    MOVE ' || " " || '
                             TO SELECCION-FINAL-CON(POS-INI-SEL-F: 11)
                    ADD  11     TO POS-INI-SEL-F
                    ADD  1      TO LARGO-REGISTRO
                 END-IF
                 ADD    1       TO CTA-CAMPO-SEL
                 UNSTRING CAMPO-ARR(POS-ARR-CAMPO) DELIMITED BY '.'
                          INTO ARCHIVO-PEPA,
                               CAMPO-PEPA
                 MOVE CAMPO-CORTO-ARR(POS-ARR-CAMPO) TO WS-FLDCORTO
                 IF WS-FLDCORTO(1:4) NOT = 'PEPA'
                    MOVE   CAMPO-ARR(POS-ARR-CAMPO)
                           TO SELECCION-FINAL(POS-INI-SEL:22)
                    ADD  22         TO POS-INI-SEL
                 ELSE
                    PERFORM ASIGNAR-PEPA
                 END-IF

                 MOVE   CAMPO-ARR-CON(POS-ARR-CAMPO)
                               TO SELECCION-FINAL-CON(POS-INI-SEL-F:40)

                 ADD  40         TO POS-INI-SEL-F
              END-IF

            END-PERFORM
            MOVE ') AS CHAR(10000))' TO
                                 SELECCION-FINAL-CON (POS-INI-SEL-F:17)

            MOVE SPACES     TO ARCHIVOS-SEL
            MOVE ' IN('     TO ARCHIVOS-SEL(1:4)
            MOVE 5          TO P-ARC-SEL

            ADD 5           TO POS-INI-SEL
            MOVE 'FROM '    TO SELECCION-FINAL(POS-INI-SEL:5)
            ADD 6           TO POS-INI-SEL

            PERFORM VARYING POS-ARR-FILE FROM 1 BY 1 UNTIL
                            POS-ARR-FILE > POS-ARR-FILE-T
                    IF POS-ARR-FILE > 1
                       MOVE ', ' TO SELECCION-FINAL(POS-INI-SEL:2)
                       ADD  2    TO POS-INI-SEL

                       MOVE ', ' TO ARCHIVOS-SEL(P-ARC-SEL:2)
                       ADD  2    TO P-ARC-SEL
                    END-IF
                    MOVE ARCHIVO-ARR(POS-ARR-FILE)
                              TO SELECCION-FINAL(POS-INI-SEL:10)
                    ADD  12   TO POS-INI-SEL

                    MOVE '"'  TO ARCHIVOS-SEL(P-ARC-SEL:1)
                    ADD   1   TO P-ARC-SEL
                    MOVE ARCHIVO-ARR(POS-ARR-FILE)
                              TO ARCHIVOS-SEL(P-ARC-SEL:10)
                    ADD  10   TO P-ARC-SEL
                    MOVE '"'  TO ARCHIVOS-SEL(P-ARC-SEL:1)
                    ADD   1   TO P-ARC-SEL

            END-PERFORM
            MOVE ') '         TO ARCHIVOS-SEL(P-ARC-SEL:2)

            MOVE 'N'        TO SAL-WS
            PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 999 OR SAL-WS = 'S'
                    IF NRO-VECES(Y) > 1
                       MOVE I-ON TO IN70 OF WC02-O-INDIC
                       MOVE 'S'  TO SAL-WS
                    END-IF
            END-PERFORM.

      *
       ASIGNAR-PEPA.
      *-------------
           OPEN INPUT ASQRY18F.

            MOVE NUMPRO-WS         TO NROPRO OF ASQRY18F
            MOVE NSUBPRO-WS        TO CORPRO OF ASQRY18F
            MOVE CAMPO-PEPA(5:4)   TO NROPEP OF ASQRY18F
            READ ASQRY18F END-READ
            IF FS-ASQRY18F = "00"
               MOVE VALPEP OF ASQRY18F  TO
                    SELECCION-FINAL(POS-INI-SEL:700)
            ELSE
               MOVE SPACES             TO
                    SELECCION-FINAL(POS-INI-SEL:700)
            END-IF
            ADD  700    TO POS-INI-SEL.

           CLOSE      ASQRY18F.
      *
       CHEQUEA-FILE-ARR.
      *-----------------
            MOVE "N"    TO EXISTE-FILE USA-ARC-RUT
            PERFORM VARYING POS-ARR-FILE FROM 1 BY 1 UNTIL
                            POS-ARR-FILE > POS-ARR-FILE-T
                    IF ARCHIVO-ARR(POS-ARR-FILE) = WS-ARCSEL
                       MOVE "S"  TO EXISTE-FILE
                    END-IF
            END-PERFORM
            IF EXISTE-FILE = "N"
               ADD    1      TO POS-ARR-FILE-T
               MOVE WS-ARCSEL  TO ARCHIVO-ARR(POS-ARR-FILE-T).
      *
       CHEQUEA-CAMPOS-ARR.
      *-------------------
            MOVE 'N'    TO EXISTE-CAMPO
            PERFORM VARYING POS-ARR-CAMPO FROM 1 BY 1 UNTIL
                            POS-ARR-CAMPO > 999
                 IF POS-ARR-CAMPO NOT = ORDEN OF WS02-OO
                    IF CAMPO-CORTO-ARR(POS-ARR-CAMPO) =
                       CAMPO OF WS02-OO
                       MOVE 'S'  TO EXISTE-CAMPO
                    END-IF
                 END-IF
            END-PERFORM
            IF EXISTE-CAMPO = 'N'
               PERFORM EVALUA-TIPO
               MOVE FLDREF OF WS02-OO TO CAMPO-ARR  (ORDEN OF WS02-OO)
               MOVE DATO-CONVERTIDO TO CAMPO-ARR-CON(ORDEN OF WS02-OO).
      *
       EVALUA-TIPO.
      *------------
           MOVE SPACES   TO DATO-CONVERTIDO
           MOVE ZEROES   TO LARGO-WS
           EVALUATE TIPO-ARR(ORDEN OF WS02-OO)
             WHEN 'CHARACTER'
                  IF LARGO-ARR(ORDEN OF WS02-OO) < 10
                     MOVE 10         TO LARGO-WS
                  ELSE
                     MOVE LARGO-ARR(ORDEN OF WS02-OO) TO LARGO-WS
                  END-IF

                  STRING ' CAST( '                   DELIMITED SIZE
                         CAMPO  OF WS02-OO           DELIMITED SIZE
                         ' AS CHARACTER('            DELIMITED SIZE
                        LARGO-WS                     DELIMITED SIZE
                         '))'                        DELIMITED SIZE
                                 INTO DATO-CONVERTIDO
             WHEN 'DECIMAL'
                  IF DIGIT-ARR(ORDEN OF WS02-OO) < 10
                     MOVE 10         TO LARGO-WS
                  ELSE
                     MOVE DIGIT-ARR(ORDEN OF WS02-OO) TO LARGO-WS
                  END-IF
                  STRING ' CAST( '                   DELIMITED SIZE
                         CAMPO  OF WS02-OO           DELIMITED SIZE
                         ' AS CHARACTER('            DELIMITED SIZE
                        LARGO-WS                     DELIMITED SIZE
                         '))'                        DELIMITED SIZE
                                 INTO DATO-CONVERTIDO
             WHEN 'NUMERIC'
                  IF DIGIT-ARR(ORDEN OF WS02-OO) < 10
                     MOVE 10         TO LARGO-WS
                  ELSE
                     MOVE DIGIT-ARR(ORDEN OF WS02-OO) TO LARGO-WS
                  END-IF
                  STRING ' CAST( '                   DELIMITED SIZE
                         CAMPO  OF WS02-OO           DELIMITED SIZE
                         ' AS CHARACTER('            DELIMITED SIZE
                         LARGO-WS                    DELIMITED SIZE
                         '))'                        DELIMITED SIZE
                                 INTO DATO-CONVERTIDO
             WHEN 'DATE'
                  MOVE   10               TO LARGO-WS
                  STRING ' CAST( '                   DELIMITED SIZE
                         CAMPO  OF WS02-OO           DELIMITED SIZE
                         ' AS CHARACTER(10'          DELIMITED SIZE
                         '))'                        DELIMITED SIZE
                                 INTO DATO-CONVERTIDO
             WHEN 'TIME'
                  MOVE   10               TO LARGO-WS
                  STRING ' CAST( '                   DELIMITED SIZE
                         CAMPO  OF WS02-OO           DELIMITED SIZE
                         ' AS CHARACTER(10'          DELIMITED SIZE
                         '))'                        DELIMITED SIZE
                                 INTO DATO-CONVERTIDO
           END-EVALUATE
           ADD LARGO-WS            TO LARGO-REGISTRO.
      *
       LEER-CURSOR-SELCUR.
      *-------------------
            EXEC SQL FETCH SELCUR INTO: REG-SQL END-EXEC.
      *
       LEER-MODIFICADO-WS01.
      *---------------------
            READ SUBFILE PANTALLA2 NEXT MODIFIED INTO WS01-OO
                                       FORMAT "WS01".

       LEER-SFL-NEXT-WS02.
      *-------------------
            READ SUBFILE PANTALLA3 INTO WS02-OO
                                       FORMAT "WS02".

      *
       LLENA-SFL-WS01.
      *---------------
             MOVE SELFLD-SQL    TO SELFLD OF WS01-OO
             MOVE SPACES        TO FLDREF OF WS01-OO

             STRING SELFLD(01:10) DELIMITED BY SPACES
                    "."           DELIMITED BY SIZE
                    SELFLD(12:10) DELIMITED BY SPACES
                    INTO          FLDREF OF WS01-OO

             MOVE SELFLD(01:10)   TO ARCHIVO OF WS01-OO
             MOVE SELFLD(12:10)   TO CAMPO   OF WS01-OO
             MOVE LARGO-SQL       TO LARGO   OF WS01-OO
             MOVE DIGIT-SQL       TO DIGIT   OF WS01-OO
             MOVE DECIM-SQL       TO DECIM   OF WS01-OO
             MOVE TIPO-SQL        TO TIPO    OF WS01-OO
             PERFORM BUSCAR-ARC-FLD
             IF SALIR-FLD = "N"
                MOVE ZEROES        TO CTL001   OF WS01-OO
                                      POSICION OF WS01-OO
             ELSE
                MOVE 1             TO CTL001   OF WS01-OO
                MOVE P-FLD         TO POSICION OF WS01-OO
             END-IF
             ADD  1               TO NREL02
             WRITE SUBFILE R-PANTALLA2 FROM WS01-OO FORMAT "WS01".
      *
       BUSCAR-ARC-FLD.
      *---------------
           MOVE 0   TO P-FLD
           MOVE "N" TO SALIR-FLD
           PERFORM UNTIL SALIR-FLD = "S" OR P-FLD = 999
               ADD 1           TO P-FLD
               IF ARCHIVO-S(P-FLD)  = ARCHIVO OF WS01-OO AND
                  CAMPO-S  (P-FLD)  = CAMPO   OF WS01-OO
                  MOVE "S"   TO SALIR-FLD
               END-IF
           END-PERFORM.
      *
       LLENA-SFL-WS02.
      *---------------
           IF ARCHIVO OF WS01-OO     = SPACES
              PERFORM ACUMULA-ARR-SA
           ELSE
              ADD  1                           TO NREL03
              MOVE ARCHIVO  OF WS01-OO         TO ARCHIVO  OF WS02-OO
                                                  ARCCAMPO OF WS02-OO
                                                  ARCHIVO-WS02(NREL03)
              MOVE SELFLD   OF WS01-OO(12: 10) TO CAMPO    OF WS02-OO
                                                  CAMPO-WS02  (NREL03)
              MOVE SELFLD   OF WS01-OO(23: 28) TO DCAMPO   OF WS02-OO
                                                  DCAMPO-WS02 (NREL03)
              MOVE POSICION OF WS01-OO         TO ORDEN    OF WS02-OO
                                                  ORDEN-WS02  (NREL03)
              MOVE FLDREF   OF WS01-OO         TO FLDREF   OF WS02-OO
                                                  FLDREF-WS02 (NREL03)
              MOVE LARGO    OF WS01-OO         TO LARGO    OF WS02-OO
                                                  LARGO-WS02  (NREL03)
              MOVE DIGIT    OF WS01-OO         TO DIGIT    OF WS02-OO
                                                  DIGIT-WS02  (NREL03)
              MOVE DECIM    OF WS01-OO         TO DECIM    OF WS02-OO
                                                  DECIM-WS02  (NREL03)
              MOVE TIPO     OF WS01-OO         TO TIPO     OF WS02-OO
                                                  TIPO-WS02   (NREL03)
              MOVE I-OFF                       TO IN80 OF WS02-O-INDIC
              WRITE SUBFILE R-PANTALLA3 FROM WS02-OO FORMAT "WS02"
                            INDICATORS ARE WS02-O-INDIC END-WRITE.

      *
       ACUMULA-ARR-SA.
      *---------------
           ADD  1        TO POS-ARR-SA
           MOVE 1        TO NRO-VECES-SA(POS-ARR-SA)

           STRING SELFLD OF WS01-OO(12:10) DELIMITED BY " "
                  "_001"                   DELIMITED SIZE
                           INTO     CAMPO-LARGO-ARR-SA(POS-ARR-SA)

           STRING SELFLD OF WS01-OO(12:10) DELIMITED BY " "
                  "001"                    DELIMITED SIZE
                          INTO      CAMPO-CORTO-ARR-SA(POS-ARR-SA)

           MOVE FLDREF OF WS01-OO  TO
                                  CAMPO-REFER-ARR-SA(POS-ARR-SA)

           MOVE LARGO  OF WS01-OO          TO LARGO-ARR-SA(POS-ARR-SA)
           MOVE DIGIT  OF WS01-OO          TO DIGIT-ARR-SA(POS-ARR-SA)
           MOVE DECIM  OF WS01-OO          TO DECIM-ARR-SA(POS-ARR-SA)
           MOVE TIPO   OF WS01-OO          TO TIPO-ARR-SA (POS-ARR-SA)
           MOVE SELFLD  OF WS01-OO(23: 28) TO DESCR-ARR-SA(POS-ARR-SA).

      *
       LIMPIA-ARR-SA.
      *--------------
            PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 999
                    MOVE ZEROES TO NRO-VECES-SA(Y)
                                   LARGO-ARR-SA(Y)
                                   DIGIT-ARR-SA(Y)
                                   DECIM-ARR-SA(Y)
                    MOVE SPACES TO CAMPO-LARGO-ARR-SA(Y)
                                   CAMPO-CORTO-ARR-SA(Y)
                                   TIPO-ARR-SA       (Y)
            END-PERFORM.

