       IDENTIFICATION DIVISION.
      *------------------------
       PROGRAM-ID.  ASQRY15.
       AUTHOR. JORGE DUARTE U.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-AS400.
       OBJECT-COMPUTER.  IBM-AS400.
       SPECIAL-NAMES. LOCAL-DATA IS LOCAL.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *-------------

           SELECT ASQRY13F    ASSIGN       TO DATABASE-ASQRY13F
                             ORGANIZATION IS INDEXED
                             ACCESS MODE  IS DYNAMIC
                             RECORD KEY   IS EXTERNALLY-DESCRIBED-KEY
                             FILE STATUS  IS FS-ASQRY13F.

           SELECT PANTALLA   ASSIGN        TO WORKSTATION-ASQRY13D-SI
                             ORGANIZATION  IS TRANSACTION
                             ACCESS MODE   IS DYNAMIC
                             RELATIVE KEY  IS NREL
                             CONTROL-AREA  IS AREA-CONTROL
                             FILE STATUS   IS FS-PANTALLA.


       DATA DIVISION.
       FILE SECTION.
      *-------------
       FD  ASQRY13F  LABEL RECORD STANDARD.
       01  R-ASQRY13F.
           COPY  DDS-ALL-FORMAT OF ASQRY13F.

       FD  PANTALLA LABEL RECORD STANDARD.
       01  R-PANTALLA           PIC X(1920).

      *
       WORKING-STORAGE SECTION.
      *------------------------
       77 NOMARC-WS             PIC X(10).
       77 BIBARC-WS             PIC X(10).
       77 CUENTA-WS             PIC S9(5).
       77 NRO-CAMPOS            PIC S9(3).
       77 CUENTA-PRO            PIC X(1000).
       77 NREL                  PIC 9(4).
       77 WS-DATECUR            PIC 9(08).
       77 I-ON                  PIC 1 VALUE B"1".
       77 I-OFF                 PIC 1 VALUE B"0".

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

       01 WS-VARIABLES-DE-TRABAJO.
          05 INSREG             PIC X(500).

       01 WS-ESTADOS.
          05 FS-ASQRY13F         PIC X(02) VALUE "00".
          05 FS-PANTALLA        PIC X(02) VALUE "00".

       01 AREA-CONTROL.
          05 MDTO               PIC 9(02).
             88 INTRO           VALUE 00.
             88 F3              VALUE 03.
             88 F5              VALUE 05.
             88 F7              VALUE 07.
             88 F9              VALUE 09.
             88 F23             VALUE 23.

       01 DATOS-SQL.
          05 CAMPO-SQL          PIC X(10).
          05 CABEC1-SQL         PIC X(10).
          05 CABEC2-SQL         PIC X(10).
          05 CABEC3-SQL         PIC X(10).

       01 PARAMETROS-26A.
           05 NOMARC-26A          PIC X(10).
           05 BIBARC-26A          PIC X(10).
           05 TEXTO-26A           PIC X(50).
           05 OPCION-26A          PIC X(01).
           05 RET-26A             PIC X(01).

       01 ARREGLOS-DE-TRABAJO.
          05 CTA-MSG              PIC 9(3).
          05 VARIABLE-3250        PIC X(6500).
          05 VAR-ARR-3250 REDEFINES VARIABLE-3250 OCCURS 100 TIMES
                          ASCENDING KEY IS TEXTO-ARR.
             10 TEXTO-ARR         PIC X(65).
      *
       01 WC01-II.
          COPY DDS-WC01-I       OF ASQRY13D.

       01 WC01-IND.
          COPY DDS-WC01-INDIC   OF ASQRY13D.

       01 WS01-OO.
          COPY DDS-WS01-O       OF ASQRY13D.

       PROCEDURE DIVISION.
      *-------------------
           PERFORM INICIO
           PERFORM PROCESO UNTIL F3
           PERFORM TERMINO
           GOBACK.

      *
       INICIO.
      *-------
           OPEN I-O PANTALLA ASQRY13F.
           INITIALIZE WC01-I REPLACING
                      ALPHANUMERIC DATA BY SPACES
                      NUMERIC      DATA BY ZEROES.
           CALL "MRTVDAF1" USING WS-DATECUR
           MOVE WS-DATECUR       TO DATECUR OF WC01-II.
           MOVE ZEROES           TO MDTO
           PERFORM CARGAR-ARCHIVOS-WS01.
      *
       PROCESO.
      *--------
           WRITE R-PANTALLA FROM WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-O-INDIC END-WRITE
           READ  PANTALLA   INTO WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG

           IF F5
              PERFORM CARGAR-ARCHIVOS-WS01
           ELSE
           IF F7
              PERFORM CAMBIAR-CAMPOS
           ELSE
           IF F23
              PERFORM ELIMINAR-CAMPO
              PERFORM CARGAR-ARCHIVOS-WS01
           ELSE
           IF F9
              PERFORM INSERTAR
              PERFORM CARGAR-ARCHIVOS-WS01.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       TERMINO.
      *-------
           CLOSE    PANTALLA ASQRY13F.
      *
       CAMBIAR-CAMPOS.
      *-----------------
           READ SUBFILE PANTALLA NEXT MODIFIED INTO WS01-OO
                        FORMAT "WS01" END-READ.
           IF FS-PANTALLA = "00"
              MOVE CORR WS01-O   TO WC01-I.

      *
       ELIMINAR-CAMPO.
      *---------------
           READ SUBFILE PANTALLA NEXT MODIFIED INTO WS01-OO
                        FORMAT "WS01" END-READ.
           IF FS-PANTALLA = "00"
              MOVE CAMPO OF WS01-OO  TO CAMPO OF ASQRY13F
              READ ASQRY13F END-READ
              IF FS-ASQRY13F = "00"
                 DELETE ASQRY13F END-DELETE
                 ADD 1                   TO CTA-MSG
                 STRING "SE HA ELIMINADO "            DELIMITED SIZE
                        CAMPO OF WS01-OO              DELIMITED BY " "
                        INTO TEXTO-ARR(CTA-MSG).
      *
       INSERTAR.
      *---------
           IF CAMPO OF WC01-II = SPACES
              ADD    1                         TO CTA-MSG
              STRING "DEBE INGRESAR NOMBRE DE CAMPO " DELIMITED SIZE
                     INTO TEXTO-ARR(CTA-MSG)
           ELSE
           IF CABEC1 OF WC01-II = SPACES
              ADD    1                         TO CTA-MSG
              STRING "DEBE INGRESAR MINIMO CABECERA 1" DELIMITED SIZE
                     INTO TEXTO-ARR(CTA-MSG)
           ELSE
              MOVE CAMPO  OF WC01-II       TO CAMPO OF ASQRY13F
              READ ASQRY13F END-READ
              IF FS-ASQRY13F = "00"
                 MOVE CORR WC01-I    TO RASQRY13F
                 REWRITE R-ASQRY13F END-REWRITE
                 ADD    1                         TO CTA-MSG
                 STRING "SE ACTUALIZO VALOR AL CAMPO " DELIMITED SIZE
                        CAMPO OF WC01-II               DELIMITED SIZE
                        INTO TEXTO-ARR(CTA-MSG)
              ELSE
                 MOVE CORR WC01-I    TO RASQRY13F
                 WRITE R-ASQRY13F END-WRITE
                 ADD    1                         TO CTA-MSG
                 STRING "SE INGRESO EL CAMPO "         DELIMITED SIZE
                        CAMPO OF WC01-II               DELIMITED SIZE
                        INTO TEXTO-ARR(CTA-MSG).
      *
       CARGAR-ARCHIVOS-WS01.
      *---------------------
           PERFORM LIMPIA-WS01

           EXEC SQL
           DECLARE C1 CURSOR FOR SELECT CAMPO, CABEC1, CABEC2, CABEC3
           FROM ASQRY13F ORDER BY CAMPO
           END-EXEC

           EXEC SQL
                OPEN C1
           END-EXEC
           PERFORM LEER-FETCH-C1
           PERFORM UNTIL SQLCODE NOT = 0
                   ADD   1                     TO NREL
                   MOVE SPACES                 TO DETSFL OF WS01-OO
                   MOVE ZEROES                 TO CTL001 OF WS01-OO
                   MOVE CAMPO-SQL              TO CAMPO  OF WS01-OO
                   MOVE CABEC1-SQL             TO CABEC1 OF WS01-OO
                   MOVE CABEC2-SQL             TO CABEC2 OF WS01-OO
                   MOVE CABEC3-SQL             TO CABEC3 OF WS01-OO
                   STRING CAMPO-SQL            DELIMITED SIZE
                          ' '                  DELIMITED SIZE
                          CABEC1-SQL           DELIMITED SIZE
                          '  '                 DELIMITED SIZE
                          CABEC2-SQL           DELIMITED SIZE
                          '  '                 DELIMITED SIZE
                          CABEC3-SQL           DELIMITED SIZE
                                            INTO DETSFL OF WS01-OO
               WRITE SUBFILE R-PANTALLA FROM WS01-OO FORMAT 'WS01'
                     END-WRITE

               PERFORM LEER-FETCH-C1
           END-PERFORM
           EXEC SQL
                CLOSE C1
           END-EXEC.
           IF NREL > 0
              MOVE I-ON           TO IN30 OF WC01-O-INDIC
                                     IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN32 OF WC01-O-INDIC
           ELSE
              MOVE I-ON           TO IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN30 OF WC01-O-INDIC
                                     IN32 OF WC01-O-INDIC.

      *
       LEER-FETCH-C1.
      *--------------
           EXEC SQL
                FETCH NEXT FROM C1 INTO :DATOS-SQL
           END-EXEC.
      *
       LIMPIA-WS01.
      *------------
           MOVE I-ON            TO IN32 OF WC01-O-INDIC
           MOVE I-OFF           TO IN30 OF WC01-O-INDIC
                                   IN31 OF WC01-O-INDIC
           MOVE ZEROES          TO NREL
           WRITE R-PANTALLA FROM WC01-II FORMAT "WC01"
                                 INDICATORS ARE WC01-O-INDIC.
