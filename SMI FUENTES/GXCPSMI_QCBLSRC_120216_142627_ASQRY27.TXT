       IDENTIFICATION DIVISION.
      *------------------------*
       PROGRAM-ID. ASQRY27.
       AUTHOR.     JORGE DUARTE U.
      *
       ENVIRONMENT DIVISION.
      *---------------------*
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.      IBM-AS400.
       OBJECT-COMPUTER.      IBM-AS400.
       INPUT-OUTPUT SECTION.
      *--------------------*
       FILE-CONTROL.
      *------------*

       DATA DIVISION.
      *-------------*
       FILE SECTION.
      *-------------*

       WORKING-STORAGE SECTION.
      *------------------------
       77 WS-DEFFLD           PIC X(180).
       77 WS-LARGO            PIC 9(03).
       77 WS-ARCGEN           PIC X(10).
       77 WS-BIBGEN           PIC X(10).
       77 WS-SECEJE           PIC S9(5).
       77 WS-NROPRO           PIC S9(5).
       77 WS-CORPRO           PIC S9(5).
       77 WS-MIEMBRO          PIC X(4000).
       77 WS-DESCONS          PIC X(50).

       77 WS-CANREG           PIC S9(8).
       77 CTA-REGISTROS       PIC X(1000).
       77 WS-SELECC           PIC X(12000).
       77 SEN-INSERT          PIC X(20000).
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.

       01 VARIABLES-DE-TRABAJO.
           05 REG-AGE-ARR-X  PIC X(9200).
           05 REG-AGE-ARR REDEFINES REG-AGE-ARR-X OCCURS 50 TIMES.
              15 REGAGE-X        PIC X(1).
              15 DEFFLD-X        PIC X(180).
              15 LARGO-9         PIC 9(3).
           05 WX-SELECC.
              10 WX-SELECC1      PIC X(9001).
              10 WX-SELECC2      PIC X(2999).

          05 AAAAMMDD         PIC X(8).
          05 AAAAMMDD-9 REDEFINES AAAAMMDD PIC S9(8).
          05 DDMMAAAA         PIC X(8).

          05 HORA-SISTEMA.
             10 HORA-SYS      PIC S9(6).
             10 FILLER        PIC S9(2).

       01 PARAMETROS-20A.
          05 NOMCONS-20A       PIC X(10).
          05 FECPRO-20A       PIC S9(8).
          05 FECPRO-20A-R REDEFINES FECPRO-20A PIC X(8).
          05 HORPRO-20A       PIC S9(6).
          05 HORPRO-20A-R REDEFINES HORPRO-20A PIC X(6).
          05 ARCBAS-20A       PIC X(10).
          05 BIBBAS-20A       PIC X(10).
          05 ARCGEN-20A       PIC X(10).
          05 SECEJE-20A       PIC X(05).
          05 SECTOP-20A       PIC X(05).
          05 REFABAS-20A      PIC X(10).
          05 REFAGEN-20A      PIC X(10).
          05 REFBGEN-20A      PIC X(10).
          05 PROREF-20A       PIC S9(5).
          05 CPRREF-20A       PIC S9(5).
          05 REEARC-20A       PIC X(01).
          05 RETJOB-20A       PIC X(01).
          05 ADDREG-20A       PIC X(01).
          05 REGUNI-20A       PIC X(01).

       01 DATOS-C200.
          05 REGAGE-C200     PIC X(1).
          05 DEFFLD-C200     PIC X(180).
          05 LARGO-C200      PIC S9(3).

       01 ARREGLO-OVRDBF.
          05 P-OVR            PIC 9(3).
          05 MIEMBRO-OVR-X    PIC X(4000).
          05 MIEMBRO-OVR-R REDEFINES MIEMBRO-OVR-X OCCURS 100 TIMES.
             10 ARCHIVO-OVR   PIC X(10).
             10 MASCARA-OVR   PIC X(10).
             10 BIBLIOT-OVR   PIC X(10).
             10 MIEMBRO-OVR   PIC X(10).

       01 MANDATO-OVR.
          05 MANDATO-200      PIC X(200).
          05 LARGO-200        PIC S9(10)V9(5) COMP-3 VALUE 200.

       01 WK-VARIABLES-EMAIL.
          05 NOMBRE-USR       PIC X(15).
          05 APEPAT-USR       PIC X(15).
          05 APEMAT-USR       PIC X(15).
          05 EMAIL-USR        PIC X(60).


          05 EMISOR-EMAIL         PIC X(0060).
          05 DESTINATARIO-EMAIL   PIC X(0700).
          05 COPIA-EMAIL          PIC X(0700).
          05 ASUNTO-EMAIL         PIC X(0300).
          05 TEXTO-EMAIL          PIC X(2000).
          05 PLANTILLA-EMAIL      PIC X(0060).
          05 VARIABLES-EMAIL      PIC X(1000).
          05 RESPUESTA-EMAIL      PIC X(0006).
          05 GLOSA-EMAIL          PIC X(0060).

       01 VARIABLES-RUTINA-DE-ACCESO.
          05 RUT-USUARIO      PIC S9(09).
          05 RUT-USUARIO1     PIC 9(09).
          05 DV-RUT-USUARIO   PIC X.
          05 P-REG-AGE        PIC 9(3).
          05 P-AUT-USR        PIC 9(3).
          05 P-STR-REG        PIC 9(5).
          05 STR-RANGO-REG-AGE PIC X(2000).
          05 PGM              PIC X(10).
          05 NRODAT           PIC 9(02).
          05 MRINI            PIC X(198).
          05 R-MRINI REDEFINES MRINI OCCURS 99 TIMES.
             10 REG-INI       PIC 9(2).
          05 MCINI            PIC X(594).
          05 R-MCINI REDEFINES MCINI OCCURS 99 TIMES.
             10 AGE-INI       PIC 9(6).
          05 MRFIN            PIC X(198).
          05 R-MRFIN REDEFINES MRFIN OCCURS 99 TIMES.
             10 REG-FIN       PIC 9(2).
          05 MCFIN            PIC X(594).
          05 R-MCFIN REDEFINES MCFIN OCCURS 99 TIMES.
             10 AGE-FIN       PIC 9(6).
          05 CODRET           PIC S9(2).


       LINKAGE SECTION.
       01 CONSULTA-LNK        PIC X(10).
       01 FECPRO-LNK          PIC S9(8).
       01 HORPRO-LNK          PIC S9(6).
       01 SECEJE-LNK          PIC S9(5).
       01 SECTOP-LNK          PIC S9(5).
       01 USUARIO-LNK         PIC X(10).
       01 ADDREG-LNK          PIC X(01).
       01 REGUNI-LNK          PIC X(01).

       PROCEDURE DIVISION USING CONSULTA-LNK
                                FECPRO-LNK
                                HORPRO-LNK
                                SECEJE-LNK
                                SECTOP-LNK
                                USUARIO-LNK
                                ADDREG-LNK
                                REGUNI-LNK.
      *----------------------------------------------*
      * NEOEXT4PF  = ARCHIVO DE USUARIOS.            *
      * NEOACHSLIB = BIBLIOTECA ARCHIVO DE USUARIOS. *
      *----------------------------------------------*
      *
           PERFORM RESCATAR-RUT

           CALL    "MRTVDAF2" USING AAAAMMDD DDMMAAAA
           CANCEL  "MRTVDAF2"
           ACCEPT HORA-SISTEMA FROM TIME

           MOVE SPACES    TO WS-SELECC WS-ARCGEN WS-BIBGEN
                             WS-MIEMBRO
           EXEC SQL
           SELECT SELEJE, ARCGEN, BIBGEN, MIEMBR INTO
           :WS-SELECC, :WS-ARCGEN, :WS-BIBGEN,
           :WS-MIEMBRO FROM ASQRY27F WHERE NOMCONS = :CONSULTA-LNK AND
           FECPRO1 = :FECPRO-LNK AND HORPRO1 = :HORPRO-LNK AND SECEJE =
           :SECEJE-LNK
           END-EXEC

           MOVE WS-SELECC  TO WX-SELECC

           IF WS-MIEMBRO NOT = " "
              MOVE WS-MIEMBRO    TO MIEMBRO-OVR-X
              PERFORM GENERA-OVR-ARCHIVOS
           END-IF

           PERFORM BUSCAR-ARC-REG-AGE
           IF WS-ARCGEN NOT = " "
              PERFORM GENERA-ARCHIVO
           END-IF
           ADD 1     TO SECEJE-LNK
           IF SECEJE-LNK NOT > SECTOP-LNK
              PERFORM EJECUTA-SGTE-PROCESO
           END-IF
      *    PERFORM ENVIAR-EMAIL
           GOBACK.
      *
       BUSCAR-ARC-REG-AGE.
      *-------------------
           EXEC SQL
           DECLARE C200 CURSOR FOR SELECT DISTINCT B.REGAGE, B.DEFFLD,
           DIGITS( CAST(LENGTH(RTRIM(B.DEFFLD)) AS NUMERIC(3, 0))) FROM
            ASQRY24F A, ASQRY31F B WHERE A.NOMCONS = :CONSULTA-LNK AND
           A.NOMARC = B.WHFILE AND B.REGAGE IN("R", "A")
           END-EXEC
           EXEC SQL  OPEN    C200  END-EXEC
           PERFORM LEER-FETCH-C200
           MOVE 1  TO P-REG-AGE
           PERFORM UNTIL SQLCODE NOT = 0 OR P-REG-AGE > 50
               MOVE REGAGE-C200  TO REGAGE-X (P-REG-AGE)
               MOVE DEFFLD-C200  TO DEFFLD-X (P-REG-AGE)
               MOVE LARGO-C200   TO LARGO-9  (P-REG-AGE)
               PERFORM LEER-FETCH-C200
               ADD  1  TO P-REG-AGE
           END-PERFORM
           EXEC SQL  CLOSE   C200  END-EXEC
           IF DEFFLD-X(1) NOT = SPACES
              PERFORM GENERAR-CONDICION-REGAGE
           END-IF
           .
      *
       LEER-FETCH-C200.
      *----------------
           EXEC SQL
                FETCH NEXT FROM C200 INTO :DATOS-C200
           END-EXEC.
      *
       RESCATAR-RUT.
      *-------------
      *
      *  SE RESCATA RUT DE USUARIO PARA LLAMAR RUTINAS DE REGIONAL/
      *  AGENCIA.
      *
           MOVE ZEROES     TO RUT-USUARIO
           MOVE SPACES     TO DV-RUT-USUARIO

           EXEC SQL
           SELECT RUTFUN, RDVFUN INTO :RUT-USUARIO, :DV-RUT-USUARIO
           FROM NEOACHSLIB/NEOEXT4PF WHERE USUARI = :USUARIO-LNK
           END-EXEC
           .
      *
       GENERAR-CONDICION-REGAGE.
      *-------------------------
      *
      *  SE RESCATA RUT DE USUARIO PARA LLAMAR RUTINAS DE REGIONAL/
      *  AGENCIA.
      *
           EXEC SQL
           SELECT RUTFUN, RDVFUN INTO :RUT-USUARIO, :DV-RUT-USUARIO
           FROM NEOACHSLIB/NEOEXT4PF WHERE USUARI = :USUARIO-LNK
           END-EXEC

           MOVE SPACES  TO STR-RANGO-REG-AGE

           MOVE "SMIREGAGE" TO PGM
           MOVE ZEROES      TO NRODAT
           MOVE RUT-USUARIO TO RUT-USUARIO1
           MOVE ALL "0"     TO   MRINI
           MOVE ALL "0"     TO   MCINI
           MOVE ALL "0"     TO   MRFIN
           MOVE ALL "0"     TO   MCFIN
           MOVE ZEROES      TO   NRODAT
           MOVE ZEROES      TO   CODRET
           CALL "PRTNRUSU" USING RUT-USUARIO1,
                                  PGM,
                                  NRODAT,
                                  MRINI,
                                  MCINI,
                                  MRFIN,
                                  MCFIN,
                                  CODRET
      *
           IF NRODAT > 0 AND DEFFLD-X(1) NOT = SPACES

           MOVE 1         TO P-REG-AGE
           MOVE 6         TO P-STR-REG
           ADD  1         TO P-STR-REG
           PERFORM UNTIL DEFFLD-X(P-REG-AGE) = SPACES
                         OR P-REG-AGE = 51
              MOVE 1     TO  P-AUT-USR
              PERFORM UNTIL P-AUT-USR > 99 OR
                            P-AUT-USR > NRODAT
               IF P-REG-AGE > 1 AND
                  P-AUT-USR = 1
                  MOVE " AND "            TO
                       STR-RANGO-REG-AGE(P-STR-REG:5)
                  ADD  6     TO  P-STR-REG
               END-IF

               MOVE DEFFLD-X(P-REG-AGE) TO  WS-DEFFLD
               MOVE LARGO-9 (P-REG-AGE) TO  WS-LARGO
                IF P-AUT-USR > 1 AND
                 ((REG-INI(P-AUT-USR) NOT = ZEROES OR
                   REG-FIN(P-AUT-USR) NOT = ZEROES)OR
                  (AGE-INI(P-AUT-USR) NOT = ZEROES OR
                   AGE-FIN(P-AUT-USR) NOT = ZEROES))
                  MOVE "OR"               TO
                       STR-RANGO-REG-AGE(P-STR-REG:2)
                  ADD  4     TO  P-STR-REG
                END-IF
                IF REGAGE-X(P-REG-AGE) = "R" AND
                  (REG-INI(P-AUT-USR) NOT = ZEROES OR
                   REG-FIN(P-AUT-USR) NOT = ZEROES)

                  MOVE WS-DEFFLD(1:WS-LARGO) TO
                       STR-RANGO-REG-AGE(P-STR-REG:WS-LARGO)
                  ADD  2        TO  P-STR-REG
                  ADD  WS-LARGO TO  P-STR-REG

                  MOVE "BETWEEN "   TO
                       STR-RANGO-REG-AGE(P-STR-REG:8)
                  ADD  10    TO  P-STR-REG
                  MOVE REG-INI(P-AUT-USR) TO
                       STR-RANGO-REG-AGE(P-STR-REG:2)
                  ADD  3     TO  P-STR-REG
                  MOVE " AND "        TO
                       STR-RANGO-REG-AGE(P-STR-REG:5)
                  ADD  5     TO  P-STR-REG
                  MOVE REG-FIN(P-AUT-USR) TO
                       STR-RANGO-REG-AGE(P-STR-REG:2)
                  ADD  3     TO  P-STR-REG

                  ADD 1      TO P-AUT-USR

                END-IF
                IF REGAGE-X(P-REG-AGE) = "A" AND
                  (AGE-INI(P-AUT-USR) NOT = ZEROES OR
                   AGE-FIN(P-AUT-USR) NOT = ZEROES)

                  MOVE WS-DEFFLD(1:WS-LARGO) TO
                       STR-RANGO-REG-AGE(P-STR-REG:WS-LARGO)
                  ADD  2        TO  P-STR-REG
                  ADD  WS-LARGO TO P-STR-REG
                  MOVE "BETWEEN "   TO
                       STR-RANGO-REG-AGE(P-STR-REG:8)
                  ADD  10    TO  P-STR-REG
                  MOVE AGE-INI(P-AUT-USR) TO
                       STR-RANGO-REG-AGE(P-STR-REG:6)
                  ADD  6     TO  P-STR-REG
                  MOVE " AND "        TO
                       STR-RANGO-REG-AGE(P-STR-REG:5)
                  ADD  5     TO  P-STR-REG
                  MOVE AGE-FIN(P-AUT-USR) TO
                       STR-RANGO-REG-AGE(P-STR-REG:6)
                  ADD  6     TO  P-STR-REG

                  ADD 1      TO P-AUT-USR

                END-IF
                IF  REG-INI(P-AUT-USR) = ZEROES AND
                    REG-FIN(P-AUT-USR) = ZEROES AND
                    AGE-INI(P-AUT-USR) = ZEROES AND
                    AGE-FIN(P-AUT-USR) = ZEROES
                    ADD 99 TO P-AUT-USR
                END-IF
              END-PERFORM
              ADD 1      TO P-REG-AGE
           END-PERFORM
           IF P-STR-REG > 7
              IF WX-SELECC2 NOT = SPACES
                 MOVE " AND ("  TO STR-RANGO-REG-AGE(1:6)
              ELSE
                 MOVE "WHERE "  TO WS-SELECC(9002:6)
                 MOVE "     ("  TO STR-RANGO-REG-AGE(1:6)
              END-IF
              MOVE ")"       TO STR-RANGO-REG-AGE(P-STR-REG:1).
           DISPLAY  "STR-RANGO.." STR-RANGO-REG-AGE.
      *
       GENERA-OVR-ARCHIVOS.
      *--------------------
           PERFORM VARYING P-OVR FROM 1 BY 1 UNTIL
                           ARCHIVO-OVR(P-OVR) = " "
               MOVE SPACES TO MANDATO-200
               STRING "OVRDBF FILE("       DELIMITED BY SIZE
                      ARCHIVO-OVR(P-OVR)   DELIMITED BY SIZE
                      ") TOFILE("          DELIMITED BY SIZE
                      BIBLIOT-OVR(P-OVR)   DELIMITED BY " "
                      "/"                  DELIMITED BY SIZE
                      MASCARA-OVR(P-OVR)   DELIMITED BY " "
                      ") MBR("             DELIMITED BY SIZE
                      MIEMBRO-OVR(P-OVR)   DELIMITED BY " "
                      ") SHARE(*YES) "     DELIMITED BY SIZE
                                       INTO MANDATO-200
               CALL  "QCMDEXC" USING MANDATO-200 LARGO-200

           END-PERFORM.
      *
       EJECUTA-SGTE-PROCESO.
      *---------------------
           PERFORM RESCATA-VOLORES-A-SOMETER.
      *
       RESCATA-VOLORES-A-SOMETER.
      *--------------------------
           EXEC SQL
           SELECT NOMCONS, FECPRO1, HORPRO1, ARCBAS,
           BIBBAS, ARCGEN, REEARC, RETJOB,  ADDREG,
           REGUNI, MIEMBR INTO :NOMCONS-20A, :FECPRO-20A, :HORPRO-20A,
           :ARCBAS-20A, :BIBBAS-20A,
           :ARCGEN-20A, :REEARC-20A, :RETJOB-20A,
           :ADDREG-20A, :REGUNI-20A FROM
            ASQRY27F WHERE NOMCONS = :CONSULTA-LNK AND FECPRO1 =
           :FECPRO-LNK AND HORPRO1 = :HORPRO-LNK AND SECEJE =
           :SECEJE-LNK
           END-EXEC

           MOVE SPACES     TO REFABAS-20A
                              REFAGEN-20A
                              REFBGEN-20A

           PERFORM RESCATA-REF-ARCHIVO

           MOVE SECEJE-LNK          TO SECEJE-20A
           MOVE SECTOP-LNK          TO SECTOP-20A
           CALL "ASQRY16P" USING NOMCONS-20A
                                 FECPRO-20A-R
                                 HORPRO-20A-R
                                 SECEJE-20A
                                 ARCBAS-20A
                                 BIBBAS-20A
                                 ARCGEN-20A
                                 USUARIO-LNK
                                 SECTOP-20A
                                 REFABAS-20A
                                 REFAGEN-20A
                                 REFBGEN-20A
                                 REEARC-20A
                                 RETJOB-20A
                                 ADDREG-20A
                                 REGUNI-20A
           CANCEL "ASQRY16P".
      *
       RESCATA-REF-ARCHIVO.
      *--------------------

           MOVE SPACES  TO REFABAS-20A

           EXEC SQL
           SELECT ARCBAS, ARCGEN, BIBGEN INTO :REFABAS-20A,
           :REFAGEN-20A, :REFBGEN-20A FROM  ASQRY27F WHERE
           NOMCONS = :CONSULTA-LNK AND FECPRO1 = :FECPRO-LNK AND
           HORPRO1 =
           :HORPRO-LNK
           END-EXEC.
      *
       GENERA-ARCHIVO.
      *---------------
      *
      * MODIFICA ARCHIVO DE CONTROL INDICANDO EN PROCESO
      *
           EXEC SQL
           UPDATE  ASQRY27F SET FECPRO2 = :AAAAMMDD-9, HORPRO2 =
           :HORA-SYS, ARCGEN = :WS-ARCGEN, BIBGEN = :WS-BIBGEN, ESTPRO
           = "P" WHERE NOMCONS = :CONSULTA-LNK AND FECPRO1 =
           :FECPRO-LNK
           AND HORPRO1 = :HORPRO-LNK AND SECEJE = :SECEJE-LNK
           END-EXEC.
      *
      * INSERTA INFORNMACION SELECCIONADA EN ARCHIVO A GENERAR
      *
            MOVE SPACES               TO SEN-INSERT

            STRING "INSERT INTO "     DELIMITED SIZE
                   WS-BIBGEN          DELIMITED BY " "
                   "/"                DELIMITED SIZE
                   WS-ARCGEN          DELIMITED BY " "
                   " "                DELIMITED SIZE
                   WS-SELECC          DELIMITED SIZE
                   " "                DELIMITED SIZE
                   STR-RANGO-REG-AGE  DELIMITED SIZE
                   " ORDER BY 1 "     DELIMITED SIZE
                                 INTO SEN-INSERT.

            EXEC SQL
                DECLARE INSERTADATOS STATEMENT
            END-EXEC
            EXEC SQL
                PREPARE INSERTADATOS FROM :SEN-INSERT
            END-EXEC.
            EXEC SQL
                EXECUTE INSERTADATOS
            END-EXEC.

      *
      * CUENTA LA CANTIDAD DE REGISTROS INSERTADOS
      *
            MOVE SPACES    TO CTA-REGISTROS
            STRING "SELECT CAST(COUNT(*)" DELIMITED SIZE
                   " AS NUMERIC (8, 0)) " DELIMITED SIZE
                   " FROM "               DELIMITED SIZE
                   WS-BIBGEN              DELIMITED BY SPACES
                   "/"                    DELIMITED SIZE
                   WS-ARCGEN              DELIMITED BY SPACES
                                     INTO CTA-REGISTROS

            EXEC SQL
               DECLARE CTAREGISTROS STATEMENT
            END-EXEC
            EXEC SQL
               PREPARE CTAREGISTROS FROM :CTA-REGISTROS
            END-EXEC
            EXEC SQL
               DECLARE CREGISTROS CURSOR FOR CTAREGISTROS
            END-EXEC
            EXEC SQL
               OPEN CREGISTROS
            END-EXEC
            EXEC SQL
               FETCH NEXT FROM CREGISTROS INTO :WS-CANREG
            END-EXEC
            EXEC SQL
               CLOSE CREGISTROS
            END-EXEC.
      *
      * MODIFICA ARCHIVO DE CONTROL INDICANDO TERMINO
      *
      *    CALL    "MRTVDAF2" USING AAAAMMDD DDMMAAAA
      *    CANCEL  "MRTVDAF2"
      *    ACCEPT HORA-SISTEMA FROM TIME.
      *
      *     EXEC SQL
      *    UPDATE  ASQRY27F SET FECPRO2 = :AAAAMMDD-9, HORPRO2 =
      *    :HORA-SYS, ARCGEN = :WS-ARCGEN, BIBGEN = :WS-BIBGEN, ESTPRO
      *    = "P", CANREG = :WS-CANREG WHERE NOMCONS = :CONSULTA-LNK AND
      *    FECPRO1 = :FECPRO-LNK AND HORPRO1 = :HORPRO-LNK AND SECEJE =
      *    :SECEJE-LNK
      *     END-EXEC.
      *
       ENVIAR-EMAIL.
      *--------------
           CALL "PNOMMAIL" USING RUT-USUARIO
                                 NOMBRE-USR
                                 APEPAT-USR
                                 APEMAT-USR
                                 EMAIL-USR

           MOVE SPACES      TO WS-DESCONS
           EXEC SQL
           SELECT DESCONS INTO :WS-DESCONS FROM ASQRY23F WHERE NOMCONS
           = :CONSULTA-LNK
           END-EXEC

           DISPLAY "EMAIL-USR...: " EMAIL-USR
           MOVE "SELECCIONES_MULTIPLES@ACHS.CL" TO EMISOR-EMAIL
           MOVE EMAIL-USR                       TO DESTINATARIO-EMAIL
      *    MOVE "FIDEL.PEREZ@VTR.NET"           TO DESTINATARIO-EMAIL
           MOVE SPACES                          TO COPIA-EMAIL
           MOVE SPACES                          TO ASUNTO-EMAIL
           STRING "FINALIZACION PROCESO (SELMUL)" DELIMITED SIZE
                  " CONSULTA:"                    DELIMITED SIZE
                  CONSULTA-LNK                    DELIMITED SIZE
                  " ("                            DELIMITED SIZE
                  WS-DESCONS                      DELIMITED SIZE
                  " )"                            DELIMITED SIZE
                                              INTO ASUNTO-EMAIL
           MOVE SPACES                          TO TEXTO-EMAIL
           STRING "CONSULTA: "              DELIMITED SIZE
                  CONSULTA-LNK              DELIMITED SIZE
                  " FECHA: "                DELIMITED SIZE
                  FECPRO-LNK                DELIMITED SIZE
                  " HORA: "                 DELIMITED SIZE
                  HORPRO-LNK                DELIMITED SIZE
                  " REGISTROS GENERADOS: "  DELIMITED SIZE
                  WS-CANREG                 DELIMITED SIZE
                  " HA FINALIZADO, REVISE SU RESULTADO."
                                            DELIMITED SIZE
                                     INTO TEXTO-EMAIL
           MOVE SPACES                          TO PLANTILLA-EMAIL
           MOVE SPACES                          TO VARIABLES-EMAIL
           MOVE SPACES                          TO RESPUESTA-EMAIL
           MOVE SPACES                          TO GLOSA-EMAIL

           CALL "MAILCLI3" USING EMISOR-EMAIL
                                 DESTINATARIO-EMAIL
                                 COPIA-EMAIL
                                 ASUNTO-EMAIL
                                 TEXTO-EMAIL
                                 PLANTILLA-EMAIL
                                 VARIABLES-EMAIL
                                 RESPUESTA-EMAIL
                                 GLOSA-EMAIL
           DISPLAY "SE ENVIO EMAIL..."
           .
