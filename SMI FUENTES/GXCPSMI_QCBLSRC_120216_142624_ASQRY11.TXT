       IDENTIFICATION DIVISION.
      *------------------------
       PROGRAM-ID.  ASQRY11.
       AUTHOR. JORGE DUARTE U.
      *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-AS400.
       OBJECT-COMPUTER.  IBM-AS400.
       SPECIAL-NAMES. LOCAL-DATA IS LOCAL.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *-------------
           SELECT ASQRY08F    ASSIGN       TO DATABASE-ASQRY08F
                             ORGANIZATION IS INDEXED
                             ACCESS MODE  IS DYNAMIC
                             RECORD KEY   IS EXTERNALLY-DESCRIBED-KEY
                             FILE STATUS  IS FS-ASQRY08F.


           SELECT PANTALLA   ASSIGN        TO WORKSTATION-ASQRY09D-SI
                             ORGANIZATION  IS TRANSACTION
                             ACCESS MODE   IS DYNAMIC
                             RELATIVE KEY  IS NREL1
                             CONTROL-AREA  IS AREA-CONTROL
                             FILE STATUS   IS FS-PANTALLA.


       DATA DIVISION.
       FILE SECTION.
      *-------------
       FD  ASQRY08F  LABEL RECORD STANDARD.
       01  REG-ASQRY08F.
           COPY  DDS-ALL-FORMAT OF ASQRY08F.

       FD  PANTALLA LABEL RECORD STANDARD.
       01  R-PANTALLA           PIC X(1920).

      *
       WORKING-STORAGE SECTION.
      *------------------------
       77 CODEVE-WS             PIC X(10).
       77 DESEVE-WS             PIC X(50).
       77 OPCION-WS             PIC S9(05).
       77 MANDATO-WS            PIC X(50).
       77 NREL1                 PIC 9(4).
       77 WS-DATECUR            PIC 9(08).
       77 I-ON                  PIC 1 VALUE B"1".
       77 I-OFF                 PIC 1 VALUE B"0".

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

       01 WS-ESTADOS.
          05 FS-ASQRY08F         PIC X(02) VALUE "00".
          05 FS-PANTALLA        PIC X(02) VALUE "00".

       01 VARIABLES-DE-TRABAJO.
          05 WS-HHMMSSSS.
             10 WS-HORA         PIC S9(6).
             10 FILLER          PIC S9(2).

          05 AAAAMMDD           PIC X(8).
          05 AAAAMMDD-9 REDEFINES AAAAMMDD PIC S9(8).
          05 DDMMAAAA           PIC X(8).

       01 AREA-CONTROL.
          05 MDTO               PIC 9(02).
             88 INTRO           VALUE 00.
             88 F3              VALUE 03.
             88 F9              VALUE 09.

       01 EVENTO-SQL.
          05 OPCION-SQL        PIC S9(05).
          05 CODEVE-SQL        PIC X(10).
          05 TEXTO-SQL         PIC X(50).
          05 MANDATO-SQL       PIC X(50).

       01 ARREGLOS-DE-TRABAJO.
          05 CTA-MSG              PIC 9(3).
          05 VARIABLE-3250        PIC X(6500).
          05 VAR-ARR-3250 REDEFINES VARIABLE-3250 OCCURS 100 TIMES
                          ASCENDING KEY IS TEXTO-ARR.
             10 TEXTO-ARR         PIC X(65).

       01  PARAM-MSG.
           05 PARAM1           PIC X(100).
           05 PARAM2           PIC S9(10)V9(5) USAGE IS COMP VALUE 100

       01 WC01-II.
          COPY DDS-WC01-I       OF ASQRY09D.

       01 WC01-IND.
          COPY DDS-WC01-INDIC   OF ASQRY09D.

       01 WS01-OO.
          COPY DDS-WS01-O       OF ASQRY09D.

      *

       LINKAGE SECTION.
       01 PARA-LNK.
          05 USUARIO-LNK      PIC X(10).

       PROCEDURE DIVISION USING PARA-LNK.
      *-----------------------------------
           PERFORM INICIO
           PERFORM PROCESO UNTIL F3
           PERFORM TERMINO
           GOBACK.

      *
       INICIO.
      *-------
           OPEN INPUT ASQRY08F
                I-O PANTALLA.
           INITIALIZE WC01-I REPLACING
                      ALPHANUMERIC DATA BY SPACES
                      NUMERIC      DATA BY ZEROES.
           CALL "MRTVDAF1" USING WS-DATECUR
           MOVE WS-DATECUR       TO DATECUR OF WC01-II.
           MOVE ZEROES           TO MDTO
                                    CTA-MSG
           PERFORM CARGAR-WS01.
      *
       PROCESO.
      *--------
           WRITE R-PANTALLA FROM WC01-II FORMAT "WC01"
                            INDICATORS ARE WC01-O-INDIC END-WRITE
           READ  PANTALLA   INTO WC01-II FORMAT "WC01"
                            INDICATORS ARE WC01-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG

           IF F9
              PERFORM EJECUTA-PROCESOS.

           PERFORM CARGAR-WS01

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       EJECUTA-PROCESOS.
      *----------------
           READ SUBFILE PANTALLA NEXT MODIFIED
                                 INTO WS01-OO FORMAT "WS01"
           END-READ
           PERFORM UNTIL FS-PANTALLA NOT = "00"
             MOVE MANDATO OF WS01-OO TO PARAM1
             CALL "QCMDEXC" USING PARAM1, PARAM2
             READ SUBFILE PANTALLA NEXT MODIFIED
                                   INTO WS01-OO FORMAT "WS01"
             END-READ
          END-PERFORM.
      *
       TERMINO.
      *-------
           CLOSE  ASQRY08F  PANTALLA.
      *
       CARGAR-WS01.
      *------------
           PERFORM LIMPIA-WS01

           EXEC SQL
           DECLARE C1 CURSOR FOR SELECT OPCION, CODEVE, TEXTO,
           MANDATO FROM ASQRY06F WHERE CODEVE NOT IN " "
           ORDER BY CODEVE
           END-EXEC

           EXEC SQL
                OPEN C1
           END-EXEC
           PERFORM LEER-FETCH-C1
           PERFORM UNTIL SQLCODE NOT = 0
                   PERFORM CHEQUEA-AUTORIZACION
                   IF FS-ASQRY08F = "00"
                      ADD   1                     TO NREL1
                      MOVE SPACES                 TO DETSFL OF WS01-OO
                      MOVE ZEROES                 TO CTL001 OF WS01-OO
                      MOVE CODEVE-SQL             TO CODEVE OF WS01-OO
                      MOVE TEXTO-SQL              TO DESEVE OF WS01-OO
                      MOVE MANDATO-SQL            TO MANDATO OF WS01-OO
                      MOVE TEXTO-SQL              TO DETSFL OF WS01-OO
                      WRITE SUBFILE R-PANTALLA FROM WS01-OO
                      FORMAT "WS01" END-WRITE
                   END-IF
               PERFORM LEER-FETCH-C1
           END-PERFORM
           EXEC SQL
                CLOSE C1
           END-EXEC.
           IF NREL1 > 0
              MOVE I-ON           TO IN30 OF WC01-O-INDIC
                                     IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN32 OF WC01-O-INDIC
           ELSE
              MOVE I-ON           TO IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN30 OF WC01-O-INDIC
                                     IN32 OF WC01-O-INDIC
              ADD 1                   TO CTA-MSG
              STRING "Usuario no está autorizado"  DELIMITED SIZE
                     INTO TEXTO-ARR(CTA-MSG).

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       CHEQUEA-AUTORIZACION.
      *----------------------
           MOVE OPCION-SQL  TO OPCION  OF ASQRY08F
           MOVE USUARIO-LNK TO USUARIO OF ASQRY08F
           READ ASQRY08F INVALID KEY CONTINUE END-READ
           IF FS-ASQRY08F NOT = "00"
              MOVE "*ALL"   TO USUARIO OF ASQRY08F
              READ ASQRY08F INVALID KEY CONTINUE END-READ.

      *
       LEER-FETCH-C1.
      *--------------
           EXEC SQL
                FETCH NEXT FROM C1 INTO :EVENTO-SQL
           END-EXEC.
      *
       LIMPIA-WS01.
      *------------
           MOVE I-ON            TO IN32 OF WC01-O-INDIC
           MOVE I-OFF           TO IN30 OF WC01-O-INDIC
                                   IN31 OF WC01-O-INDIC
           MOVE ZEROES          TO NREL1
           WRITE R-PANTALLA FROM WC01-II FORMAT "WC01"
                                INDICATORS ARE WC01-O-INDIC.
      *
