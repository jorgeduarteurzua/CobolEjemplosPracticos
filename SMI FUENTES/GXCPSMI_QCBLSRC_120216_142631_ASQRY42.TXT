       IDENTIFICATION DIVISION.
      *-----------------------*
       PROGRAM-ID. ASQRY42.
       AUTHOR. JORGE DUARTE U.
       ENVIRONMENT DIVISION.
      *--------------------*
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.      IBM-AS400.
       OBJECT-COMPUTER.      IBM-AS400.
       SPECIAL-NAMES.        LOCAL-DATA IS LOCAL-DATA-AREA .
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

       DATA DIVISION.
      *--------------*
       FILE SECTION.
      *-------------*

       WORKING-STORAGE SECTION.
      *------------------------*
       77 P-ARR                PIC 9(4).
       77 P-33                 PIC 9(4).
       77 SEL-C1               PIC X(2500).
       77 WS-ARCHIVO           PIC X(10).
       77 EXISTE-CAMPO         PIC X(1).

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

       01 DATOS-CURSOR-C1.
          05 ARCHIVO-C1        PIC X(10).
          05 CAMPO-C1          PIC X(10).

       01 ARREGLOS-DE-TRABAJO.
          05 RR-CAM-DEF-ARR PIC X(2000).
          05 CAMPOS-DEF-ARR REDEFINES RR-CAM-DEF-ARR OCCURS 100 TIMES.
             10 ARCHIVO-D        PIC X(10).
             10 CAMPO-D          PIC X(10).

          05 ARCH-CAMPOS-33  PIC X(2000).
          05 CAMPOS-DEF-33  REDEFINES ARCH-CAMPOS-33 OCCURS 100 TIMES.
             10 ARCHIVO-33       PIC X(10).
             10 CAMPO-33         PIC X(10).
      *
       LINKAGE SECTION.
      *----------------
       01 CONSULTA-LNK    PIC X(10).
       01 CONCEPTOS-LNK   PIC X(250).
       01 ARCH-CAMPOS-LNK PIC X(2000).
       01 CONDICION-LNK   PIC X(2000).

       PROCEDURE DIVISION USING CONSULTA-LNK,
                                CONCEPTOS-LNK,
                                ARCH-CAMPOS-LNK,
                                CONDICION-LNK.
      *--------------------------------------------------------

           MOVE SPACES             TO ARCH-CAMPOS-LNK
           MOVE SPACES             TO CONDICION-LNK

           INSPECT CONSULTA-LNK    CONVERTING '.' TO ' '
           INSPECT CONCEPTOS-LNK   CONVERTING '.' TO ' '
           INSPECT ARCH-CAMPOS-LNK CONVERTING '.' TO ' '
           INSPECT CONDICION-LNK   CONVERTING '.' TO ' '

           CALL  'ASQRY33' USING CONCEPTOS-LNK ARCH-CAMPOS-33

           MOVE 1            TO P-ARR
           MOVE SPACES       TO SEL-C1

           EXEC SQL
             SELECT CONSQL INTO :CONDICION-LNK FROM ASQRY25F WHERE
             NOMCONS = :CONSULTA-LNK
           END-EXEC

           STRING 'SELECT DISTINCT B.WHFILE, B.WHFLDI FROM ASQRY25F A, '
                                                 DELIMITED SIZE
                  ' ASQRY01F B '                 DELIMITED SIZE
                  ' WHERE A.NOMCONS = "'         DELIMITED SIZE
                  CONSULTA-LNK                   DELIMITED SIZE
                  '" AND A.ARCBAS = B.WHFILE '   DELIMITED SIZE
                                              INTO SEL-C1
           EXEC SQL
                DECLARE SELC1 STATEMENT
           END-EXEC
           EXEC SQL
                PREPARE SELC1 FROM :SEL-C1
           END-EXEC
           EXEC SQL
                DECLARE C1 CURSOR FOR SELC1
           END-EXEC.

           MOVE 0 TO P-ARR

           EXEC SQL OPEN     C1    END-EXEC
           PERFORM FETCH-CURSOR-C1
           PERFORM UNTIL SQLCODE NOT = 0 OR P-ARR = 100
                 PERFORM BUSCAR-CAMPO-33
                 IF EXISTE-CAMPO = "S"
                    ADD 1   TO P-ARR
                    MOVE WS-ARCHIVO    TO ARCHIVO-D (P-ARR)
                    MOVE CAMPO-C1      TO CAMPO-D   (P-ARR)
                 END-IF
                 PERFORM FETCH-CURSOR-C1

           END-PERFORM
           MOVE RR-CAM-DEF-ARR TO ARCH-CAMPOS-LNK
           EXEC SQL CLOSE    C1    END-EXEC.
           GOBACK.
      *
       BUSCAR-CAMPO-33.
      *----------------
           MOVE "N"          TO EXISTE-CAMPO
           PERFORM VARYING P-33 FROM 1 BY 1 UNTIL P-33 > 100
                   OR EXISTE-CAMPO = "S"
               IF CAMPO-C1 = CAMPO-33(P-33)
                   MOVE "S"  TO EXISTE-CAMPO
                   MOVE ARCHIVO-33(P-33)  TO WS-ARCHIVO
               END-IF
           END-PERFORM.
      *
       FETCH-CURSOR-C1.
      *---------------
           EXEC SQL
                FETCH NEXT FROM C1 INTO :DATOS-CURSOR-C1
           END-EXEC.
