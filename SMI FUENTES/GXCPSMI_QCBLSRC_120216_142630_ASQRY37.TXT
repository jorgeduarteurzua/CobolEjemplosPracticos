       IDENTIFICATION DIVISION.
      *------------------------
       PROGRAM-ID.  ASQRY37.
       AUTHOR. JORGE DUARTE U.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-AS400.
       OBJECT-COMPUTER.  IBM-AS400.
       SPECIAL-NAMES. LOCAL-DATA IS LOCAL.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *-------------

           SELECT PANTALLA   ASSIGN        TO WORKSTATION-ASQRY27D-SI
                             ORGANIZATION  IS TRANSACTION
                             ACCESS MODE   IS DYNAMIC
                             RELATIVE KEY  IS NREL
                             FILE STATUS   IS FS-PANTALLA.

           SELECT PANTALLA1  ASSIGN        TO WORKSTATION-ASQRY27D-SI
                             ORGANIZATION  IS TRANSACTION
                             ACCESS MODE   IS DYNAMIC
                             RELATIVE KEY  IS NREL1
                             FILE STATUS   IS FS-PANTALLA1.


       DATA DIVISION.
       FILE SECTION.
      *-------------
       FD  PANTALLA LABEL RECORD STANDARD.
       01  R-PANTALLA           PIC X(1920).

       FD  PANTALLA1 LABEL RECORD STANDARD.
       01  R-PANTALLA1          PIC X(1920).

      *
       WORKING-STORAGE SECTION.
      *------------------------
       77 USR-USAR              PIC X(10).
       77 PERFIL-NOMBRE         PIC X(500).
       77 WS-CODGRP             PIC X(10).
       77 WS-DESGRP             PIC X(50).
       77 WS-PERFIL             PIC X(10).
       77 WS-DESUSR             PIC X(50).
       77 NREL                  PIC 9(4).
       77 NREL1                 PIC 9(4).
       77 WS-DATECUR            PIC 9(08).
       77 I-ON                  PIC 1 VALUE B"1".
       77 I-OFF                 PIC 1 VALUE B"0".
       77 ARR-MAR-CONC       PIC X(6000).

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

       01 WS-VARIABLES-DE-TRABAJO.
          05 CUENTA-USR          PIC S9(05).
          05 P1                  PIC 9(05).
          05 I                   PIC 9(05).

       01 WS-ESTADOS.
          05 FS-PANTALLA        PIC X(02) VALUE "00".
          05 FS-PANTALLA1       PIC X(02) VALUE "00".

      *
       01 PARA-ASQRY07.
          05 NOMAYU-18C          PIC X(10).
          05 SELECC-18C          PIC X(500).
          05 LARGOKEY-18C        PIC 9(03).
          05 LARGODES-18C        PIC 9(03).
          05 RETORNO-18C         PIC X(30000).
          05 MARCAR-18C          PIC X(30000).

       01 AREA-CONTROL.
          05 MDTO               PIC 9(02).
             88 INTRO           VALUE 00.
             88 F3              VALUE 03.
             88 F5              VALUE 05.
             88 F7              VALUE 07.
             88 F8              VALUE 08.
             88 F9              VALUE 09.
             88 F11             VALUE 11.
             88 F12             VALUE 12.
             88 F15             VALUE 15.
             88 F23             VALUE 23.

       01 DATOS-SQL.
          05 CODGRP-SQL         PIC X(10).
          05 DESGRP-SQL         PIC X(50).

       01 DATOS-SQL2.
          05 PERFIL-SQL2        PIC X(10).
          05 NOMUSR-SQL2        PIC X(50).

       01 ARREGLOS-DE-TRABAJO.
          05 ARR-SEL-X          PIC X(30000).
          05 ARR-SEL REDEFINES ARR-SEL-X OCCURS 240 TIMES.
             15 CODIGO-X        PIC X(125).

          05 ARR-MAR-X          PIC X(30000).
          05 ARR-MAR REDEFINES ARR-MAR-X OCCURS 240 TIMES.
             15 CODMAR-X        PIC X(125).

          05 CTA-MSG              PIC 9(3).
          05 VARIABLE-3250        PIC X(6500).
          05 VAR-ARR-3250 REDEFINES VARIABLE-3250 OCCURS 100 TIMES
                          ASCENDING KEY IS TEXTO-ARR.
             10 TEXTO-ARR         PIC X(65).

      *
       01 WC00-II.
          COPY DDS-WC00-I       OF ASQRY27D.

       01 WC00-IND.
          COPY DDS-WC00-INDIC   OF ASQRY27D.

       01 WS00-OO.
          COPY DDS-WS00-O       OF ASQRY27D.

      *
       01 WC01-II.
          COPY DDS-WC01-I       OF ASQRY27D.

       01 WC01-IND.
          COPY DDS-WC01-INDIC   OF ASQRY27D.

       01 WS01-OO.
          COPY DDS-WS01-O       OF ASQRY27D.

      *  ASQRY33F GRUPOS DE USUARIOS
      *  ASQRY34F USUARIOS X GRUPOS
      *
      *
       LINKAGE SECTION.
       01 USUARIO-LNK    PIC X(10).

       PROCEDURE DIVISION USING USUARIO-LNK.
      *-------------------------------------
           PERFORM INICIO
           PERFORM PROCESO UNTIL IN03 OF WC00-I-INDIC = I-ON
           PERFORM TERMINO
           GOBACK.

      *
       INICIO.
      *-------
           OPEN I-O PANTALLA.
           INITIALIZE WC01-I REPLACING
                      ALPHANUMERIC DATA BY SPACES
                      NUMERIC      DATA BY ZEROES.
           CALL "MRTVDAF1" USING WS-DATECUR
           MOVE WS-DATECUR        TO DATECUR   OF WC00-II
           MOVE SPACES            TO GRPUSRC00 OF WC00-II
           MOVE I-OFF             TO IN03 OF WC00-I-INDIC
                                     IN05 OF WC00-I-INDIC
                                     IN06 OF WC00-I-INDIC
                                     IN12 OF WC00-I-INDIC
                                     IN23 OF WC00-I-INDIC

           MOVE SPACES           TO WS-CODGRP
           PERFORM CARGAR-WS00.
      *
       PROCESO.
      *--------
           WRITE R-PANTALLA FROM WC00-II FORMAT "WC00"
                             INDICATORS ARE WC00-O-INDIC END-WRITE
           READ  PANTALLA   INTO WC00-II FORMAT "WC00"
                             INDICATORS ARE WC00-I-INDIC END-READ.

           MOVE    SPACES        TO VARIABLE-3250
           MOVE    ZEROES        TO CTA-MSG

           IF IN05 OF WC00-I-INDIC = I-ON
              PERFORM MOSTRAR-USUARIO-DE-GRUPO
              MOVE SPACES               TO WS-CODGRP
              PERFORM CARGAR-WS00
           ELSE
           IF IN23 OF WC00-I-INDIC = I-ON
              PERFORM ELIMINAR-GRUPO
              MOVE SPACES               TO WS-CODGRP
              PERFORM CARGAR-WS00
           ELSE
           IF IN03 OF WC00-I-INDIC = I-OFF
              MOVE GRPUSRC00 OF WC00-II TO WS-CODGRP
              PERFORM CARGAR-WS00.

           IF CTA-MSG NOT = ZEROES
              CALL    "VISERR2" USING VARIABLE-3250
              CANCEL  "VISERR2"
           END-IF.
      *
       TERMINO.
      *-------
           CLOSE    PANTALLA.
      *
       MOSTRAR-USUARIO-DE-GRUPO.
      *-------------------------
           OPEN I-O    PANTALLA1.
           INITIALIZE WC01-II REPLACING ALPHANUMERIC DATA BY SPACES
                                             NUMERIC DATA BY ZEROES

           MOVE I-OFF              TO IN85   OF WC01-O-INDIC
           READ SUBFILE PANTALLA NEXT MODIFIED INTO WS00-OO
                        FORMAT "WS00" END-READ
           IF FS-PANTALLA = "00"
              MOVE GRPUSR OF WS00-OO  TO WS-CODGRP
                                         GRPUSR OF WC01-II
              MOVE DESGRP OF WS00-OO  TO WS-DESGRP
                                      DESGRPUSR OF WC01-II
              MOVE SPACES             TO WS-PERFIL
              MOVE I-ON               TO IN85   OF WC01-O-INDIC
              PERFORM CARGAR-WS01
              MOVE GRPUSR OF WS00-OO  TO GRPUSR OF WC01-II
              MOVE DESGRP OF WS00-OO  TO DESGRPUSR OF WC01-II
           ELSE
              MOVE SPACES             TO GRPUSR OF WC01-II
                                         DESGRPUSR OF WC01-II
                                         WS-CODGRP
                                         WS-DESGRP
              MOVE I-ON           TO IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN30 OF WC01-O-INDIC
                                     IN32 OF WC01-O-INDIC
           END-IF
           MOVE 00                TO MDTO
           MOVE I-OFF             TO IN03 OF WC01-I-INDIC
                                     IN05 OF WC01-I-INDIC
                                     IN06 OF WC01-I-INDIC
                                     IN12 OF WC01-I-INDIC
                                     IN23 OF WC01-I-INDIC

           PERFORM PEDIR-WC01 UNTIL IN12 OF WC01-I-INDIC = I-ON
           CLOSE    PANTALLA1.
      *
       ELIMINAR-GRUPO.
      *----------------
           READ SUBFILE PANTALLA NEXT MODIFIED INTO WS00-OO
                        FORMAT "WS00" END-READ
           IF FS-PANTALLA = "00"
              MOVE GRPUSR OF WS00-OO  TO WS-CODGRP
              MOVE DESGRP OF WS00-OO  TO WS-DESGRP
              MOVE 0                  TO CUENTA-USR
              EXEC SQL
           SELECT CAST(COUNT(*) AS NUMERIC(5, 0)) INTO :CUENTA-USR FROM
           ASQRY34F WHERE CODGRP = :WS-CODGRP
              END-EXEC

              IF CUENTA-USR > 0
                 ADD 1                   TO CTA-MSG
                 STRING "DEBE ELIMINAR LOS USUARIOS " DELIMITED SIZE
                     "DEL GRUPO PRIMERO."          DELIMITED SIZE
                     INTO TEXTO-ARR(CTA-MSG)
              ELSE
                 EXEC SQL
                      DELETE FROM ASQRY33F WHERE CODGRP = :WS-CODGRP
                 END-EXEC
              END-IF
           END-IF.

      *
       CARGAR-WS01.
      *------------
           PERFORM LIMPIA-WS01
           INITIALIZE WC01-I REPLACING
                      ALPHANUMERIC DATA BY SPACES
                      NUMERIC      DATA BY ZEROES.
           EXEC SQL
           DECLARE C2 CURSOR FOR SELECT PERFIL, NOMUSR FROM ASQRY34F
           WHERE CODGRP = :WS-CODGRP AND PERFIL >= :WS-PERFIL ORDER BY
           PERFIL
           END-EXEC

           EXEC SQL
                OPEN C2
           END-EXEC
           PERFORM LEER-FETCH-C2
           PERFORM UNTIL SQLCODE NOT = 0
                   ADD   1                     TO NREL1
                   MOVE SPACES                 TO DETSFL   OF WS01-OO
                   MOVE ZEROES                 TO CTL001   OF WS01-OO
                   MOVE PERFIL-SQL2            TO CODUSR   OF WS01-OO
                   MOVE NOMUSR-SQL2            TO DESUSR   OF WS01-OO

                   STRING PERFIL-SQL2          DELIMITED SIZE
                          " "                  DELIMITED SIZE
                          NOMUSR-SQL2          DELIMITED SIZE
                          "  "                 DELIMITED SIZE
                                          INTO DETSFL  OF WS01-OO
                 WRITE SUBFILE R-PANTALLA1 FROM WS01-OO FORMAT "WS01"
                         END-WRITE

               PERFORM LEER-FETCH-C2
           END-PERFORM
           EXEC SQL
                CLOSE C2
           END-EXEC

           IF NREL1 > 0
              MOVE I-ON           TO IN30 OF WC01-O-INDIC
                                     IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN32 OF WC01-O-INDIC
           ELSE
              MOVE I-ON           TO IN31 OF WC01-O-INDIC
              MOVE I-OFF          TO IN30 OF WC01-O-INDIC
                                     IN32 OF WC01-O-INDIC
           END-IF.
      *
       PEDIR-WC01.
      *-----------
           MOVE WS-CODGRP  TO GRPUSR    OF WC01-II
           MOVE WS-DESGRP  TO DESGRPUSR OF WC01-II

           WRITE R-PANTALLA1 FROM WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-O-INDIC END-WRITE
           READ  PANTALLA1   INTO WC01-II FORMAT "WC01"
                             INDICATORS ARE WC01-I-INDIC END-READ.

           MOVE GRPUSR    OF WC01-II TO WS-CODGRP
           MOVE DESGRPUSR OF WC01-II TO WS-DESGRP

           IF IN06 OF WC01-I-INDIC = I-ON
              PERFORM INSERT-GRUPO
              PERFORM INSERT-USUARIO
              MOVE SPACES               TO WS-PERFIL
              PERFORM CARGAR-WS01
           ELSE
              IF IN23 OF WC01-I-INDIC = I-ON
                 PERFORM DELETE-USUARIO
                 MOVE SPACES               TO WS-PERFIL
                 PERFORM CARGAR-WS01
              ELSE
                 IF IN12 OF WC01-I-INDIC = I-OFF
                    MOVE CODUSRC01 OF WC01-II TO WS-PERFIL
                    PERFORM CARGAR-WS01
                 END-IF
              END-IF
           END-IF.
      *
       INSERT-GRUPO.
      *---------------
           MOVE GRPUSR OF WC01-II    TO WS-CODGRP
           MOVE DESGRPUSR OF WC01-II TO WS-DESGRP
           IF WS-CODGRP NOT = SPACES
           EXEC SQL
           INSERT INTO GXDBSMI/ASQRY33F VALUES(:WS-CODGRP, :WS-DESGRP)
           END-EXEC.
      *
       INSERT-USUARIO.
      *---------------
           STRING 'SELECT DISTINCT '          DELIMITED SIZE
                  'CAST( PERFIL AS CHAR('     DELIMITED SIZE
                  '125)) FROM ASQRY34F WHERE' DELIMITED SIZE
                  ' CODGRP = "'               DELIMITED SIZE
                  WS-CODGRP                   DELIMITED SIZE
                  '" '                        DELIMITED SIZE
                                   INTO  ARR-MAR-CONC

           EXEC SQL
                DECLARE USREXCEPCION  STATEMENT
           END-EXEC
           EXEC SQL
                PREPARE USREXCEPCION  FROM :ARR-MAR-CONC
           END-EXEC
           EXEC SQL
                DECLARE USRMARCAR CURSOR FOR USREXCEPCION
           END-EXEC

           EXEC SQL OPEN USRMARCAR END-EXEC
           EXEC SQL
              FETCH NEXT FROM USRMARCAR FOR 240 ROWS INTO :ARR-MAR
           END-EXEC
           MOVE ARR-MAR-X          TO MARCAR-18C
           EXEC SQL CLOSE USRMARCAR END-EXEC

              MOVE 'USUARIOS  '       TO NOMAYU-18C
              MOVE SPACES             TO SELECC-18C
              MOVE SPACES             TO RETORNO-18C
              MOVE ZEROES             TO LARGOKEY-18C
                                         LARGODES-18C

              CALL   'ASQRY07' USING NOMAYU-18C
                                      SELECC-18C
                                      LARGOKEY-18C
                                      LARGODES-18C
                                      RETORNO-18C
                                      MARCAR-18C

              CANCEL 'ASQRY07'
              IF RETORNO-18C NOT = SPACES
                 MOVE RETORNO-18C TO ARR-SEL-X
                 MOVE 1      TO I P1
                 PERFORM CON-ARCHIVOS-SEL VARYING I FROM 1 BY 1 UNTIL
                         I > 240.
      *
       CON-ARCHIVOS-SEL.
      *-----------------
           IF CODIGO-X(I) NOT = SPACES
              MOVE CODIGO-X(I)        TO PERFIL-NOMBRE
              MOVE PERFIL-NOMBRE(1:LARGOKEY-18C) TO WS-PERFIL
              MOVE PERFIL-NOMBRE((LARGOKEY-18C + 1):LARGODES-18C)
                                                  TO WS-DESUSR
              EXEC SQL
                 INSERT INTO ASQRY34F VALUES(:WS-CODGRP, :WS-PERFIL,
                 :WS-DESUSR)
              END-EXEC
              ADD  12                TO  P1.

      *
       DELETE-USUARIO.
      *---------------
           READ SUBFILE PANTALLA1 NEXT MODIFIED INTO WS01-OO
                        FORMAT "WS01" END-READ
           PERFORM UNTIL FS-PANTALLA1 NOT = "00"
               MOVE CODUSR  OF WS01-OO  TO WS-PERFIL
               EXEC SQL
                   DELETE FROM ASQRY34F WHERE CODGRP = :WS-CODGRP AND
                   PERFIL = :WS-PERFIL
               END-EXEC

               READ SUBFILE PANTALLA1 NEXT MODIFIED INTO WS01-OO
                           FORMAT "WS01" END-READ
           END-PERFORM.
      *
       CARGAR-WS00.
      *------------
           PERFORM LIMPIA-WS00

           EXEC SQL
           DECLARE C1 CURSOR FOR SELECT CODGRP, DESGRP FROM ASQRY33F
           WHERE CODGRP >= :WS-CODGRP ORDER BY CODGRP
           END-EXEC

           EXEC SQL
                OPEN C1
           END-EXEC
           PERFORM LEER-FETCH-C1
           PERFORM UNTIL SQLCODE NOT = 0
                   ADD   1                     TO NREL
                   MOVE SPACES                 TO DETSFL OF WS00-OO
                   MOVE ZEROES                 TO CTL001 OF WS00-OO
                   MOVE CODGRP-SQL             TO GRPUSR OF WS00-OO
                   MOVE DESGRP-SQL             TO DESGRP OF WS00-OO
                   STRING CODGRP-SQL           DELIMITED SIZE
                          ' '                  DELIMITED SIZE
                          DESGRP-SQL           DELIMITED SIZE
                                            INTO DETSFL OF WS00-OO
               WRITE SUBFILE R-PANTALLA FROM WS00-OO FORMAT 'WS00'
                     END-WRITE

               PERFORM LEER-FETCH-C1
           END-PERFORM
           EXEC SQL
                CLOSE C1
           END-EXEC.
           IF NREL > 0
              MOVE I-ON           TO IN30 OF WC00-O-INDIC
                                     IN31 OF WC00-O-INDIC
              MOVE I-OFF          TO IN32 OF WC00-O-INDIC
           ELSE
              MOVE I-ON           TO IN31 OF WC00-O-INDIC
              MOVE I-OFF          TO IN30 OF WC00-O-INDIC
                                     IN32 OF WC00-O-INDIC.

      *
       LEER-FETCH-C1.
      *--------------
           EXEC SQL
                FETCH NEXT FROM C1 INTO :DATOS-SQL
           END-EXEC.
      *
       LEER-FETCH-C2.
      *--------------
           EXEC SQL
                FETCH NEXT FROM C2 INTO :DATOS-SQL2
           END-EXEC.
      *
       LIMPIA-WS00.
      *------------
           MOVE I-ON            TO IN32 OF WC00-O-INDIC
           MOVE I-OFF           TO IN30 OF WC00-O-INDIC
                                   IN31 OF WC00-O-INDIC
           MOVE ZEROES          TO NREL
           WRITE R-PANTALLA FROM WC00-II FORMAT "WC00"
                                 INDICATORS ARE WC00-O-INDIC.
      *
       LIMPIA-WS01.
      *------------
           MOVE I-ON            TO IN32 OF WC01-O-INDIC
           MOVE I-OFF           TO IN30 OF WC01-O-INDIC
                                   IN31 OF WC01-O-INDIC
           MOVE ZEROES          TO NREL1
           WRITE R-PANTALLA1 FROM WC01-II FORMAT "WC01"
                                 INDICATORS ARE WC01-O-INDIC.
